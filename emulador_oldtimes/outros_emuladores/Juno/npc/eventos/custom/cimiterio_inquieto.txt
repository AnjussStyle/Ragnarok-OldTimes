//===== Cronus Script ======================================= 
//= Quest (Cimitério Inquieto) de Prontera
//===== By: ================================================== 
//= Lupus
//===== Versão Atual: ===================================== 
//= 1.2a
//===== Comentarios Adicionais: ================================= 
//= Traduzido por Kindred
//= Corrigido o erro do additem [Kamper]
//============================================================

prontera.gat,3,3,3	script	Uneasy_Check	-1,{
	end;

OnHour00:
	set $UNEASY_DL,$UNEASY_DL-1;
	set $UNEASY_BL,$UNEASY_BL+30;
	if ($UNEASY_BL>666) set $UNEASY_BL,666;
	if ($UNEASY_DL < 0) goto L_Start_Undead;
	disablenpc "Mother Mathana";
	end;
OnInit:
	if ($UNEASY_DL >= 0) disablenpc "Mother Mathana";
	end;

OnHour06:
	killmonsterall "prontera.gat";	//Todos os mortos morrem ào verem a luz do dia
	end;

OnHour01:
	if ($@UNEASY_MOB > 0) mapannounce "prontera.gat","[Madre Mathana]: Em nome de Odin! Acabe com essas sobras de mortos!!!",0;
	end;

OnZombieDead:
	set $@UNEASY_MOB,$@UNEASY_MOB-1;
	if ($@UNEASY_MOB>0) end;
	set $UNEASY_DL,0;
	set $UNEASY_H$,strcharinfo(0);
	if (Sex==1) mapannounce "prontera.gat","[Madre Mathana]: Bravo "+$UNEASY_H$+" Acabou de matar o último zumbi de Prontera!",0;
	if (Sex==0) mapannounce "prontera.gat","[Madre Mathana]: A Guerreira "+$UNEASY_H$+" Acabou de matar o último zumbi de Prontera!",0;
	set JobExp,JobExp+100;
	set BaseExp,BaseExp+50;
	end;

L_Start_Undead:
	killmonsterall "prontera.gat";	//Mata os monstros restantes
	enablenpc "Mother Mathana";
//Chama os monstros para a cidade
	set $@UNEASY_MOB, 65;
	monster "prontera.gat",0,0,"Zombie",1015,30,"Uneasy_Check::OnZombieDead";
//No cemitériio
	monster "prontera.gat",268,349,"Zombie",1015,30,"Uneasy_Check::OnZombieDead";
	monster "prontera.gat",269,350,"Ghoul",1036,5,"Uneasy_Check::OnZombieDead";
//Anúncio
	mapannounce "prontera.gat","[Madre Mathana]: O cemitério acordou!  Em nome de Odin! Corram para o santuário! Salvem Prontera!",0;
}

prontera.gat,257,313,5	script	Mother Mathana	79,{
	show "[Madre Mathana]";
	if ($UNEASY_DL <= 0) goto L_Undead_Walk;
	show "Sinto que há algo de errado em nosso cemitério";
	if ($UNEASY_H$==strcharinfo(0)) show "Mas graças à você, "+$UNEASY_H$+", nós poderemos dormir novamente " + $UNEASY_DL + " nights!";
	if ($UNEASY_H$!=strcharinfo(0)) show "Mas graças à ajude de "+$UNEASY_H$+" Nós teremos " + $UNEASY_DL + " noites calmas!";
	emotion 0;
	close;

L_Undead_Walk:
	if ($UNEASY_DL == 0) show "ELES podem retornar novamente amanhã!";
	if ($UNEASY_DL == 0 && $UNEASY_H$==strcharinfo(0)) show "Obrigado, "+$UNEASY_H$+"! Agora nós poderemos dormir até a meia-noite de amanhã!";
	if ($UNEASY_DL == 0 && $UNEASY_H$!=strcharinfo(0)) show "Ainda bem que "+$UNEASY_H$+" ajudou, agora poderemos dormir em paz!";
	show "Para acalmar a fúria desse cemitério nós devemos jogar Água Benta em todas as covas, mas está faltando Água Benta";
	show "Você pode nos ajudar dando mais?";
	next;
	menu "Sim, tome toda a minha Água Benta!",-, "Não... Estou sem.",M_NO, "Não tenho nenhuma.",M_DONT_HAVE;

	if ( countitem("Holy_Water")<1 ) goto M_DONT_HAVE;
	set $UNEASY_BL,$UNEASY_BL-countitem("Holy_Water");
	delitem "Holy_Water",countitem("Holy_Water");

	if ( $UNEASY_BL > 0 ) goto L_NEED_MORE;
//Dias calmos, sem mais zumbis para perturbar
	set $UNEASY_DL,5+((0-$UNEASY_BL)/30);
	set $UNEASY_H$,strcharinfo(0);
	show "[Madre Mathana]";
	show "Obrigado, "+$UNEASY_H$+"! Agora temos Água benta suficiente!";
	next;
	show "[Madre Mathana]";
	show "No momento que encharcarmos o cemitério com Água benta teremos " + $UNEASY_DL + " noites calmas!";
	next;
	killmonsterall "prontera.gat";	//kills any left monsters
	show "[Madre Mathana]";
	show "Viu, "+ $UNEASY_H$ +"? Eles já forma embora!";
	next;
	show "[Madre Mathana]";
	show "A nossa igreja irá agradecê-lo pessoalmente...";
	next;
	if (Sex==1) mapannounce "prontera.gat","[Madre Mathana]: Em nome de Odin declaro o Bravo "+$UNEASY_H$+" como o salvador de prontera!",0;
	if (Sex==0) mapannounce "prontera.gat","[Madre Mathana]: Em nome de Odin declaro a Bela "+$UNEASY_H$+" como a salvadora de prontera!",0;
	show "[Madre Mathana]";
	show "Em nome de Odin, eu te abençôo e te presenteio com este modesto Presente de Mareusis.";
	additem 505,1;
	set JobExp,JobExp+100;
	set BaseExp,BaseExp+50;
	close;

L_NEED_MORE:
	show "[Madre Mathana]";
	show "Obrigado "+strcharinfo(0)+", mas ainda precisamos de " + $UNEASY_BL + " mais garrafas de Água Benta.";
	close;

M_NO:
	show "[Madre Mathana]";
	show "Eu temo que o cemitério acordará logo se não tivermos água benta suficiente! Corra! Pegue todas as garrafas que encontrar!";
	close;

M_DONT_HAVE:
	show "[Mother Mathana]";
	show "Ainda precisamos de " + $UNEASY_BL + " mais garrafas...  Porque você não vai procurar mais garrafas com outras pessoas?.";
	show "The old cemetery is going out of control soon...";
	show "Please, in the name of Odin, help Prontera city.";
	close;
}
