//===== Cronus Script ======================================== 
//= Mestra das Classes
//===== Por: =================================================
//= Kamper(Criador) e Mehah (Otimização e Atualizações)
//===== Versão Atual: ======================================== 
//= 1.1
//===== Descrição: =========================================== 
//= Muda a classe do personagem
//==== Changelog ============================================
//= 1.1 Adicionado suporte a nova classe (Tae Kwon Dô) [Mehah]
//============================================================
prontera.gat,153,186,5	script	Mestra das Classes	770,{
// ======================================== Configuração =============================================
set @SupAprLvl, 45;	// Nível de Base necessário para se transformar em Super aprendiz (Padrão : 45)
set @TransLvl,99;	// Nível de Base necessário para se transformar em Aprendiz T. (Padrão : 99)
set @TransJob,50;	// Nível de Job necessário para se transformar em Aprendiz T. (Padrão : 50)
set @ZenyReq1,0;	// Dinheiro necessário para pagar a transformação de classes 1-1 e 1-1a (Padrão : 0)
set @ZenyReq2,0;	// Dinheiro necessário para pagar a transformação de classes (2-1 e 2-2)(2-1-1 e 2-2-1) (Padrão : 0)
set @ZenyReqS,0;	// Dinheiro necessário para pagar a transformação de Super Aprendiz (Padrão : 0)
set @RetItemZeny,1;	// Para pedir a retiração dos Items e Zeny ponha 1, se não ponha 0 (Padrão: 1)
set @SKPCheck,1;	// Checa se possui mais de 0 em SkillPoint [1] Não Checa [0] (Padrão: 1)
// ===================================================================================================
if(!BaseJob)
	set @Njob,10; // Nível de Job necessário para se transformar em Classe 1-1 e 1-1a. (Padrão : 10)
	else
	set @Njob,40; // Nível de Job necessário para se transformar em Classe (2-1 e 2-2)(2-1-1 e 2-2-1). (Padrão : 40)
// ===================================================================================================
function check;
function jobselect;
function questskill;
	show "[Mestra das Classes]";
	if((Class >= Job_Lord_Knight && Class <= Job_Paladin2) || Class >= 4030 && Class <= 4045|| Class == Job_SuperNovice || Class >= Job_Star_Gladiator && Class <= Job_Soul_Linker){
		show "Você já chegou ao final das classes, me desculpa mas não posso fazer nada por você.";
	}else
	if(!BaseJob && Class != Job_Novice_High){
		show "Qual classe deseja se transformar?";
		next;
		switch ( select(""+callfunc("classname",1,0,Sex)+"",""+callfunc("classname",2,0,Sex)+"",""+callfunc("classname",3,0,Sex)+"",""+callfunc("classname",4,0,Sex)+"",""+callfunc("classname",5,0,Sex)+"",""+callfunc("classname",6,0,Sex)+"",""+callfunc("classname",23,0,Sex)+"",""+callfunc("classname",4046,0,Sex)+"") ) {
			case 1: jobselect 1,@ZenyReq1;
			case 2: jobselect 2,@ZenyReq1;
			case 3: jobselect 3,@ZenyReq1;
			case 4: jobselect 4,@ZenyReq1;
			case 5: jobselect 5,@ZenyReq1;
			case 6: jobselect 6,@ZenyReq1;
			case 7: check "BaseLevel",@SupAprLvl;
				jobselect callfunc("checkclass",4),@ZenyReqS;
			case 8: jobselect 4046,@ZenyReq1;
		}
	}else
		if((BaseJob >= Job_Swordman && BaseJob <= Job_Thief) && callfunc("checkclass",5) || Class == Job_Taekwon){
			show "Qual classe deseja se transformar?";
			next;
			switch(BaseJob){
			case Job_Swordman:
					switch ( select(""+callfunc("classname",7,0,Sex)+"",""+callfunc("classname",14,0,Sex)+"") ) {
						case 1: jobselect 7,@ZenyReq2;
						case 2: jobselect 14,@ZenyReq2;
					}
			case Job_Mage:
					switch ( select(""+callfunc("classname",9,0,Sex)+"",""+callfunc("classname",16,0,Sex)+"") ) {
						case 1: jobselect 9,@ZenyReq2;
						case 2: jobselect 16,@ZenyReq2;
					}
			case Job_Archer:
					switch ( select(""+callfunc("classname",11,0,Sex)+"",""+callfunc("classname",19,0,Sex)+"") ) {
						case 1: jobselect 11,@ZenyReq2;
						case 2: if(!(sex)){jobselect 20,@ZenyReq2;}else{jobselect 19,@ZenyReq2;}
					}
			case Job_Acolyte:
					switch ( select(""+callfunc("classname",8,0,Sex)+"",""+callfunc("classname",15,0,Sex)+"") ) {
						case 1: jobselect 8,@ZenyReq2;
						case 2: jobselect 15,@ZenyReq2;
					}
			case Job_Merchant:
					switch ( select(""+callfunc("classname",10,0,Sex)+"",""+callfunc("classname",18,0,Sex)+"") ) {
						case 1: jobselect 10,@ZenyReq2;
						case 2: jobselect 18,@ZenyReq2;
					}
			case Job_Thief:
					switch ( select(""+callfunc("classname",12,0,Sex)+"",""+callfunc("classname",17,0,Sex)+"") ) {
						case 1: jobselect 12,@ZenyReq2;
						case 2: jobselect 17,@ZenyReq2;
					}					
			case Job_Taekwon:
					switch ( select(""+callfunc("classname",4047,0,Sex)+"",""+callfunc("classname",4049,0,Sex)+"") ) {
						case 1: jobselect 4047,@ZenyReq2;
						case 2: jobselect 4049,@ZenyReq2;
					}
			}
		}else
			if(Class > 6 && Class <= 20){
				questskill("save");
				show "Você tem certeza que deseja se transforma em Aprendiz T.?";
				next;
				switch(select("Sim","Não")){
					case 1:
						check "BaseLevel",@TransLvl;
						check "JobLevel",@TransJob;
						if((@RetItemZeny == 1) && Zeny || Weight){
							show "Seu dinheiro e seus itens te ligam ao mundo material.";
							show "Se livre deles para que eu possa lhe transformar em Aprendiz T.";
							close;
						}
						if(checkcart() || checkfalcon() || checkriding() ){
							if(checkcart()) show "Por favor, se livre de seu carrinho para continuarmos."; else
							if(checkfalcon()) show "Desculpe, mas você deve retirar seu falcão"; else
							if(checkriding()) show "Desculpe, mas você deve retirar seu PecoPeco";
							close;
						}
						check "SkillPoint",@SKPCheck;
						set MC_ULTJOB, Class;
						setjob Job_Novice_High;
						resetlvl(1);
						questskill("load");
				}
			}else
				if(Class == Job_Novice_High && MC_ULTJOB){
					check "JobLevel",@Njob;
					check "SkillPoint",@SKPCheck;
					show "Deseja se transformar em "+callfunc("classname",callfunc("checkclass",1),0,Sex)+"?";
					next;
					switch(select("Sim","Não")){
						case 1:
							if(@ZenyReq1){
								check "Zeny",@ZenyReq1;
								set zeny, Zeny - @ZenyReq1;
							}
							setjob callfunc("checkclass",1);
					}
				}else
					if(Class >= Job_Swordman_High && Class <= Job_Thief_High && MC_ULTJOB){
						check "JobLevel",@Njob;
						check "SkillPoint",@SKPCheck;
						show "Deseja se transformar em "+callfunc("classname",callfunc("checkclass",2),0,Sex)+"?";
						switch(select("Sim","Não")){
							case 1:
								if(@ZenyReq2){
									check "Zeny",@ZenyReq2;
									set zeny, Zeny - @ZenyReq2;
								}
								setjob callfunc("checkclass",2);
								set MC_ULTJOB, 0;
						}
					}else
							show "Erro: O seu personagem não possui a ultima classe gravada.";
	close;
function	jobselect	{
	show "[Mestra das Classes]";
	check "JobLevel",@Njob;
	check "SkillPoint",@SKPCheck;
	show "Você tem certeza?";
	next;
	switch(select("Sim","Não")){
	case 1:
		check "Zeny",getarg(1);
		set zeny, Zeny - getarg(1);
		setjob getarg(0)+callfunc("checkclass",3);
	}
	close;
}

function	questskill	{
	if(getarg(0) == "save"){
		if(getskilllv(144)) set @QSK144,1;
		if(getskilllv(156)) set @QSK156,1;
		if(getskilllv(157)) set @QSK157,1;
		if(getskilllv(153)) set @QSK153,1;
		if(getskilllv(147)) set @QSK147,1;
		if(getskilllv(149)) set @QSK149,1;
		if(getskilllv(145)) set @QSK145,1;
		if(getskilllv(154)) set @QSK154,1;
		if(getskilllv(148)) set @QSK148,1;
		if(getskilllv(150)) set @QSK150,1;
		if(getskilllv(146)) set @QSK146,1;
		if(getskilllv(155)) set @QSK155,1;
		if(getskilllv(151)) set @QSK151,1;
		if(getskilllv(152)) set @QSK152,1;	
	}else
		if(getarg(0) == "load"){
			if(@QSK144 != 0) skill 144,1,0;
			if(@QSK156 != 0) skill 156,1,0;
			if(@QSK157 != 0) skill 157,1,0;
			if(@QSK153 != 0) skill 153,1,0;
			if(@QSK147 != 0) skill 147,1,0;
			if(@QSK149 != 0) skill 149,1,0;
			if(@QSK145 != 0) skill 145,1,0;
			if(@QSK154 != 0) skill 154,1,0;
			if(@QSK148 != 0) skill 148,1,0;
			if(@QSK150 != 0) skill 150,1,0;
			if(@QSK146 != 0) skill 146,1,0;
			if(@QSK155 != 0) skill 155,1,0;
			if(@QSK151 != 0) skill 151,1,0;
			if(@QSK152 != 0) skill 152,1,0;
		}
}

function	check	{
	if(getarg(0) == "BaseLevel" && BaseLevel < getarg(1))
		show "Desculpe, mas você precisa chegar ao nível de base "+getarg(1)+"!";
	else
	if(getarg(0) == "JobLevel" && JobLevel < getarg(1))
		show "Desculpe, mas você precisa chegar ao nível de classe "+getarg(1)+"!";
	else
	if(getarg(0) == "SkillPoint" && SkillPoint > 0 && getarg(1))
		show "Desculpe, mas você precisa gastar todos os seus pontos de habilidades...";
	else
	if(getarg(0) == "Zeny" && Zeny < getarg(1)){
		show "Desculpe, mas você não tem o dinheiro necessário!";
		show "Você precisa de "+ getarg(1) +" Zeny!";
	}else
	return;
	close;
}

}

function	script	checkclass	{
	switch(getarg(0)){	
	case 1:	if(MC_ULTJOB == 7 || MC_ULTJOB == 14) return 4002; else
		if(MC_ULTJOB == 8 || MC_ULTJOB == 15) return 4005; else
		if(MC_ULTJOB == 9 || MC_ULTJOB == 16) return 4003; else
		if(MC_ULTJOB == 10 || MC_ULTJOB == 18) return 4006; else
		if(MC_ULTJOB == 11 || MC_ULTJOB == 19 || MC_ULTJOB == 20) return 4004; else
		if(MC_ULTJOB == 12 || MC_ULTJOB == 17) return 4007; else
		return 0;
	case 2: return MC_ULTJOB + 4001;	
	case 3: if (class >= Job_Baby && class <= Job_Super_Baby) return Job_Baby; else return 0;
	case 4: if (class == Job_Baby) return 22; else return 23;
	case 5: if (class >= Job_Swordman_High && class <= Job_Thief_High)
			return 0;
		else
			return 1;
	}
}
