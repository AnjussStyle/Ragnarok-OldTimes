//===== Cronus Script ================================================= 
//= Reino De Valquíria
//===== Por: ==========================================================
//= Nana, reparos por Poki
//= terminado, otimizado e testado por Lupus
//===== Versão Atual: ================================================= 
//= 3.0
//===== Descrição: ==================================================== 
//= Npcs de Valquíria
//===== Changelog: ==================================================== 
//= 2.1 Traduzido por Kosonome
//= 2.2 Renomeado Valkyrie para Valquíria [Kamper]
//= 2.3 Traduzido textos que faltava [Kamper]
//= 3.0 Script totalmente reformulado [Pudim]
//=  -  Nova estrutura e tradução refeita
//= 3.1 Corrigido mensagens com getarg(3) [Kamper]
//=====================================================================

// -== Warp's necessários! ==-
valkyrie.gat,48,73,0	warp	valk01	1,1,valkyrie.gat,48,64,0
valkyrie.gat,48,66,0	warp	valk01	1,1,valkyrie.gat,48,75,0

// -== Livro de Ymir (Pora do Céu) ==-
yuno_in02.gat,93,207,0	script	Livro de Ymir	111,{
	show "[Livro de Ymir]";
	if(Upper==2 || baseClass == Job_Taekwon || BaseJob == Job_Gunslinger || BaseJob == Job_Ninja) goto L_IGNORE; //don't allow Taekwondo classes, Baby Classes, and GS/NJ
	if(valkyrie_Q >= 1) goto L_CONT;
L_IGNORE:
	show ". . . . .";
	close;

L_CONT:
	show "*O livro se abre enquanto você se aproxima dele.*";
	next;
	show "[Livro de Ymir]";
	show "Aqueles que procuram pela fortaleza final neste mundo se arriscarão nas dificuldades deixadas pela humanidade. Arriscarão-se com épocas difíceis, ajudado por amigos amados, para finalmente depois de muito trabalho e comprometimento conseguirão enxergar a verdade sobre o mundo e então... ir além dele.";
	next;
	show "[Livro de Ymir]";
	if(valkyrie_Q == 1){
		show "*Depois de ler as duas primeiras páginas você vira a folha e vê as palavras sumindo enquanto você as tenta ler então...";
	}else{
		show "*Depois de ler as duas primeiras páginas você vira a folha e continua lendo...*";
		next;
		show "[Livro de Ymir]";
		show "É quando os Deuses e Deusas desse mundo vêem te examinar sem você perceber. Lendo sua mente, pensamentos e sentimentos verdadeiros do seu coração.";
		show "Se os Deuses, descidos ao munda há muito tempo atrás, aceitam você no hall de Valquíria, acima das nuvens no sagrado reino de Valquíria, na luz dos Deuses você renascerá, e será enviado devolta para encarar o mundo mais uma vez.";
		next;
		show "[Livro de Ymir]";
		show "E apenas depois então, o aprovarão-o para a mais nobre obediencia.. No calor de Valquíria, os Deuses te dão as boas vindas.";
		next;
		show "[Livro de Ymir]";
		show "*Enquanto você fecha o livro um estranho raio de luz te cerca-o e você se deixa render pela luz. Tuod a sua volta começa a desaparecer e tudo que resta é um branco onde você acaba passando.*";
		warp "valkyrie",48,9;
	}
	close;
}

// -== O Bibliotecário que guarda o "Livro de Ymir" ==-
yuno_in02.gat,91,176,5	script	Bibliotecário	754,{
	show "[Bibliotecário]";
	if(valkyrie_Q == 1){
		show "Por Que? você já me pagou.";
		show "Agora vá procurar pelo Livro de Ymir.";
		close;
	}
	if(BaseJob >= Job_Knight && Upper==0) goto L_PAY;
	show "Olhe em volta, mas não toque no livro. Apenas os escolhidos podem ler suas profundas palavras.";
	close;

L_PAY:
	show "Vejo que você é um humano digno.";
	show "EU, O septuagésimo oitavo bibliotecário da ordem secreta tenho o dever de proteger esse livro, Não o impedirei de vê-lo.";
	show "Antes que eu permita a você ver este maravilhoso livro, você terá de fazer duas coisas para mim.";
	next;
	show "[Bibliotecário]";
	show "Primeiro. Você terá de me pagar 1,285,000 Zeny. Lembre que você precisa de exatamente essa quantia. Nào menos nem mais que 1,285,000z.";
	show "Segundo. Você tem que arriscar no profundo interior do castelo de Juno, no coração de Ymir.";
	show "Lembre-se que você não poderá ver o livro antes de ter feito isso.";
	show "Agora Vá bravo guerreiro!.";
	menu "Pagar Agora",-,"Fechar",M_EXIT;

	show "[Bibliotecário]";
	if(Zeny != 1285000){
		show "Você não me trouxe que o número exato de zeny que eu disse. Traga-me exatamente 1,285,000 zeny!";
		show "Nada mais, nada menos.";
		close;
	}
	set Zeny,0;
	set valkyrie_Q,1;
	show "Vá agora, para o Coração de Ymir";
	show "Lá, você irá encontrar a ultima parte da informação antes de você poder abrir o livro.";
M_EXIT:
	close;
}

// -== O Coração de Ymir ==-
yuno_in05.gat,50,44,0	script	Coração de Ymir	111,{
	show "[Coração de Ymir]";
	if(valkyrie_Q == 1){
		show "Assim sobre épocas difíceis e quando nosso respeito próprio estiver lá embaixo, então é quando nossa fé tem de ser a mais forte.";
		show "Há muito tempo os guardiões secretos do caminho para o paraíso têm protegido o portal secreto da entrada de criaturas más.";
		show "Assim, ao longo do tempo a ligação começou a se enfraquecer... Então quando a Legião dos Sábios começaram a recrutar jovens, bravos e espertos "+callfunc("classname",2,1,1)+" e "+callfunc("classname",16,1,1)+", ensinando a eles as leis e os propósitos do nosso mundo.";
		next;
		show "[Coração de Ymir]";
		show "Os "+callfunc("classname",16,1,1)+" se tornaram mais fortes em ambos, mente e força, até que tudo começou a ser temido no mundo dos monstros.";
		show "Agora, após ouvir minhas palavras da sabedoria, siga até o Livro de Ymir e deixe-se iluminar pela luz dos Deuses.";
		show "Se o seu coração for puro e sem intenções malvadas, o portão irá se abrir e te levar para dentro...";
		set valkyrie_Q,2;
		close;
	}
	if(valkyrie_Q == 2){
		show "Não tenho mais nada para te ensinar, ou te dizer.";
		close;
	}
	show ". . .";
	close;
}

// -== Pesquisador do Livro de Ymir ==-
yuno_in02.gat,90,77,4	script	Pesquisador#06	744,{
	show "[Pesquisador]";
	show "Argh, where is it!?";
	show "Disseram que estaria aqui em algum lugar...";
	show "Talvez eu deva procurar mais adentro do castelo...";
	emotion e_what;
	close;
}

// -== Valhallana ==-
valkyrie.gat,48,86,6	script	Valquíria	811,{
	show "[Valhallana]";
	if(BaseJob == Job_SuperNovice) goto L_SN;
	if(baseClass == Job_Taekwon || BaseJob == Job_Gunslinger || BaseJob == Job_Ninja) goto L_TAEKWON; //sent back any Taekwondo/GS/NJ classes
	if(Upper == 2) goto L_BABY;
	if(Upper > 0) goto L_ALREADY;
	if(BaseLevel >= 99 && JobLevel >= 50 && BaseJob >= 7) goto L_CHANGE;
	if(BaseJob <= Job_Thief) goto L_NOTHING;

	show "Você precisa de Nível de Base 99 e Nível de Classe 50.";
	show "E você precisa se livrar de seu dinheiro e de todos os seus itens.";
	next;
	goto L_NOTHING;

L_BABY:
	show "Que bebê lindo?! Como você chegou aqui?";
	show "Te levarei devolta para seus pais.";
	emotion e_what;
	next;
L_Y:
	warp "yuno_in02.gat",93,204;
	close;

L_TAEKWON:
	show "Como você chegou aqui?";
	emotion e_what;
	next;
	goto L_Y;

L_SN:
	show "Bem vindo a Valquíria, "+strcharinfo(0)+".";
	show "Vejo que você seguiu o difícil caminho de um Super Aprendiz.";
	next;
	show "[Valhallana]";
	show "Me desculpe, mas eu não posso te ajuda.";
	emotion e_sry;
	next;
	goto L_NOTHING;

L_ALREADY:
	show "Não posso mais te ajudar.";
	if(Class == Job_Novice_High) show "Você já renasceu.";
	if(Class >= Job_Swordman_High && Class <= Job_Thief_High) show "Por favor vá ali, e fala com a pessoa que representa sua classe.";
	next;

L_NOTHING:
	show "Deseja voltar ao seu mundo?";
	next;
	menu "Sim",L_Y,"Não",-;

	show "[Valhallana]";
	show "As you wish...";
	close;

L_CHANGE:
	show "Bem vindo a Valquíria, "+strcharinfo(0)+". A partir deste ponto, você não pode mais volta.";
	next;
	show "[Valhallana]";
	if(Zeny || Weight) goto L_ZENYITEMS;
	if(checkcart()) goto L_CART;
	if(checkfalcon()) goto L_FALCON;
	if(checkriding()) goto L_PECO;
	if(skillpoint > 0) goto L_SKILLPNTS;

	show "Vamos começar a cerimônia da reencarnação...";
	next;

	set ADVJOB, Class+Job_Novice_High; //memo the target 3rd Job ID
	if(ADVJOB == Job_Lord_Knight2) set ADVJOB,Job_Lord_Knight;
	if(ADVJOB == Job_Paladin2) set ADVJOB,Job_Paladin;

	callfunc("F_SaveQuestSkills");

	show "[Valhallana]";
	show "Pronto...";
	show "Boa sorte.";
	setjob Job_Novice_High;
	resetlvl(1);
	callfunc "F_ClearJobVar";
	set RES_SKILL,0; //we reset Reset Skills NPC counter
	next;
	if(ADVJOB == Job_Lord_Knight || ADVJOB == Job_Paladin) goto L_Izl;
	if(ADVJOB == Job_High_Priest || ADVJOB == Job_Champion) goto L_Pro;
	if(ADVJOB == Job_High_Wizard || ADVJOB == Job_Professor) goto L_Gef;
	if(ADVJOB == Job_Whitesmith || ADVJOB == Job_Creator) goto L_Alb;
	if(ADVJOB == Job_Sniper || ADVJOB == Job_Clown || ADVJOB == Job_Gypsy) goto L_Pay;
	if(ADVJOB == Job_Assassin_Cross || ADVJOB == Job_Stalker) goto L_Mor;

L_Pro:
	savepoint "prontera.gat",273,354;
	warp "prontera.gat",273,354;
	close;

L_Mor:
	savepoint "morocc.gat",160,94;
	warp "morocc.gat",160,94;
	close;

L_Alb:
	savepoint "alberta.gat",116,57;
	warp "alberta.gat",116,57;
	close;

L_Pay:
	savepoint "payon.gat",155,90;
	warp "payon.gat",155,90;
	close;

L_Izl:
	savepoint "izlude.gat",94,103;
	warp "izlude.gat",94,103;
	close;

L_Gef:
	savepoint "geffen.gat",120,100;
	warp "geffen.gat",120,100;
	close;

L_ZENYITEMS:
	show "Seu dinheiro e itens te prente a vida comun.";
	show "Vo deve se livrar deles.";
	close;

L_CART:
	show "Por favor, desative seu carrinho e iremos continuar.";
	close;

L_FALCON:
	show "Por favor, liberte seu falcão e iremos continuar.";
	close;

L_PECO:
	show "Por favor, liberte seu PecoPeco e iremos continuar.";
	close;

L_SKILLPNTS:
	show "Você precisa usar todos os seus pontos de habilidades para podermos continuar.";
	close;
}

// function HIGH NOVICE -> HIGH 1
//getarg(0) - High Job ID
//getarg(1) - High Job Name
//getarg(2) - 3rd Job ID
//getarg(3) - current NPC's name

function	script	F_ToHigh	{
	if(Upper == 2) return; //Baby Class - skip it
	if(ADVJOB == Job_Lord_Knight2) set ADVJOB,Job_Lord_Knight;
	if(ADVJOB == Job_Paladin2) set ADVJOB,Job_Paladin;
	set @fjob,ADVJOB; //alternative classes should pass, too
	if(@fjob == Job_Stalker) set @fjob,Job_Assassin_Cross;
	if(@fjob == Job_Creator) set @fjob,Job_Whitesmith;
	if(@fjob == Job_Clown || @fjob == Job_Gypsy) set @fjob,Job_Sniper;
	if(@fjob == Job_Paladin) set @fjob,Job_Lord_Knight;
	if(@fjob == Job_Champion) set @fjob,Job_High_Priest;
	if(@fjob == Job_Professor) set @fjob,Job_High_Wizard;

	if(Class == getarg(0)) goto L_WELCOME; //3rd Job
	if(Class >= Job_Lord_Knight && Class <= Job_Paladin2) goto L_ALREADY; //already advanced class
	if(Class == Job_Novice_High && @fjob == getarg(2)) goto L_GETHIGH; //High Novice -> High XXXX
	if(Class == Job_Novice_High) {
		show "["+getarg(8)+"]";
		show "Olá, "+callfunc("classname",4001,0,1)+"! Se você está indo se tornar um "+getarg(1)+", visite seu primeiro professor de classe.";
		close;
	}
	return; //this char doesn't want to get HIGH class

L_GETHIGH:
	show "["+getarg(8)+"]";
	if(JobLevel < 10) goto L_NOTREADY;
	if(checkcart()) goto L_CART;
	if(checkfalcon()) goto L_FALCON;
	if(checkriding()) goto L_PECO;
	if(skillpoint > 0) goto L_SKILLPNTS;

	show "Olá, "+strcharinfo(0)+"!";
	show "Você fez uma brava escolhe vindo aqui para renascer e caminha em direção a um nível espiritual maior.";
	show "Agora... feche os olhos.";
	next;
	show "["+getarg(8)+"]";
	show "Abra seus olhos.";
	show "Você virou "+getarg(1)+".";
	setjob getarg(0); //High Class
	next;
	if(ADV_QSK){
		show "["+getarg(8)+"]";
		show "Deixe me te dar algumas habilidades de quest que você perdeu no processo de renascimento, "+strcharinfo(0)+".";
		next;
		//return learnt quest skills
		callfunc("F_Load1Skills");
	}
	show "["+getarg(8)+"]";
	show "Te desejo uma grande fortuna no futuro próximo!";
	emotion e_grat;
	close;

L_NOTREADY:
	show "Você não está pronto para se tornar "+getarg(1)+".";
	show "Seu Nível de Classe deve ser 10.";
	emotion e_gasp;
	close;

L_SKILLPNTS:
	show "Você precisa usar todos os seus pontos de habilidades para podermos continuar.";
	emotion e_hmm;
	close;

L_CART:
	show "Por favor, desative seu carrinho e iremos continuar.";
	emotion e_hmm;
	close;

L_FALCON:
	show "Por favor, liberte seu falcão e iremos continuar.";
	emotion e_hmm;
	close;

L_PECO:
	show "Por favor, liberte seu PecoPeco e iremos continuar.";
	emotion e_hmm;
	close;

L_WELCOME:
	show "["+getarg(8)+"]";
	show "Seja Bem vindo, "+strcharinfo(0)+"!";
	show "Estamos contentes de ver um "+getarg(1)+" aqui!";
	close;

L_ALREADY:
	show "["+getarg(8)+"]";
	show "É uma grande honra saudar enviados de Valhalla.";
	show "Aparece.";
	emotion e_ho;
	close;
}

// function GET 3rd JOB
//getarg(0) - High Job ID
//getarg(1) - High Job Name
//getarg(2) - 3rd Job ID
//getarg(3) - 3rd Job Name
function	script	F_Rebirth	{
	show "["+getarg(3)+"]";
	if(Upper == 2) goto L_BABY; //Baby Class - skip it
	if(Class >= Job_Lord_Knight && Class <= Job_Paladin2) goto L_ALREADY; //already advanced class
	if(Class == getarg(0) && ADVJOB == getarg(2)) goto L_GET3RD; //Hight XXXX -> 3rd Job
	show "Go talk to either Valhallana or one of my collegues...";
	emotion e_sry;
	close;

L_GET3RD:
	if(JobLevel < 45) goto L_NOTREADY;
	if(checkcart()) goto L_CART;
	if(checkfalcon()) goto L_FALCON;
	if(checkriding()) goto L_PECO;
	if(skillpoint > 0) goto L_SKILLPNTS;

	show "Parabéns!";
	show "Você tem treinado bastante. Agora de uma volta por ai, "+getarg(3)+"!";
	setjob getarg(2); //Rebirth Class
	set ADVJOB,0;
	if(ADV_QSK2){
		next;

		show "["+getarg(3)+"]";
		show "Deixe me te dar algumas habilidades de quest que você perdeu no processo de renascimento, "+strcharinfo(0)+".";
		next;
		//return learnt quest skills
		callfunc("F_Load2Skills");
	}
	close;

L_NOTREADY:
	show "Você veio de longe até aqui. Mas me desculpe, você ainda não está pronto para se tornar "+getarg(3)+".";
	show "You need at least Job Level 45 or higher.";
	emotion e_gasp;
	close;

L_SKILLPNTS:
	show "Você precisa usar todos os seus pontos de habilidades para podermos continuar.";
	emotion e_hmm;
	close;

L_CART:
	show "Por favor, desative seu carrinho e iremos continuar.";
	emotion e_hmm;
	close;

L_FALCON:
	show "Por favor, liberte seu falcão e iremos continuar.";
	emotion e_hmm;
	close;

L_PECO:
	show "Por favor, liberte seu PecoPeco e iremos continuar.";
	emotion e_hmm;
	close;

L_ALREADY:
	show "Olá! Você ja renasceu uma vez, não há segunda chance.";
	emotion e_ho;
	close;

L_BABY:
	show "Que bebê lindo!";
	show "Como você chegou aqui? Vá até a Valhallana e peça para ela te levar para casa.";
	emotion e_pat;
	close;
}

// function GUIDE player to Valhalla for getting 3rd JOB
// this function prevents passing 2nd JOB QUESTS by advanced classes also
//getarg(0) - High Job ID
//getarg(1) - High Job Name
//getarg(2) - 3rd Job ID
//getarg(3) - 3rd Job Name
//getarg(4) - NPC Name
function	script	F_BlockHigh	{
	if(Upper != 1) return; //This func should interact with Advanced classes only
	show "["+getarg(4)+"]";
	if(Class == getarg(0) && ADVJOB == getarg(2)) goto L_RIGHTHIGH;
	if(Class == getarg(2)) goto L_RIGHT3RD;
	if(Class >= Job_Lord_Knight && Class <= Job_Paladin2) goto L_ALREADY3RD; //already advanced class, but from wrong guild
//this player is a High Novice
	if(Class == Job_Novice_High) show "OlÁ, Aprendiz T.! Se você está indo se tornar "+getarg(1)+", então visite seu primeiro professor de classe.";
//this player has 1st advanced job, but from wrong guild
	if(Class == getarg(0)) show "Um "+getarg(1)+"?";
	if(Class != Job_Novice_High) show "Rumores dizem que a Valhallana conhece seu caminho...";
	emotion e_sry;
	close;

L_RIGHT3RD:
	show "Olá! Você parece... jovem.";
	emotion e_hmm;
	next;
	show "["+getarg(4)+"]";
	show "Você sempre será bem vindo aqui, "+strcharinfo(0)+"!";
	show "Nossa boa e velha guilda é sua segunda casa, não é?";
	emotion e_no1;
	close;

L_RIGHTHIGH:
	show "Olá, "+strcharinfo(0)+"!";
	show "Se você está indo se tornar um "+getarg(3)+", então visite Valhalla denovo.";
	if(JobLevel < 45) show "Mas você precisa de no mínimo Nível de Classe 45.";
	emotion e_gasp;
	close;

L_ALREADY3RD:
	show "A "+getarg(3)+"?";
	show "Você não pertence a nossa guilda!";
	emotion e_omg;
	close;
}