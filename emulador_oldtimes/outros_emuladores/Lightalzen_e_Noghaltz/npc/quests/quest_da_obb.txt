//===== Cronus Script =======================================
//= Quest da velha caixa Azul
//===== By ==================================================
//= Celesta, Lupus, Maluko
//===== Traduzido pro português =============================
//= Por Maluko			(www.maluko.cjb.net)
//===== Version =============================================
//= 2.0
//===== Compativel com ======================================
//= Qualquer Versão
//===== Descrição ===========================================
//= Quest Oficial da OBB. Você pode fazer quantas vezes quiser.
//= 1.6 Arrumado, fixado e traduzido pro inglês [Lupus]
//= 1.6a Arrumado os artigos requeridos para fazer uma obb, 
//= agradecimentos a marquis007  [MasterOfMuppets]
//= 2.0 Traduzido do Inglês pro português [Maluko]
//===== Description =========================================

alberta.gat,115,204,5	script	Turista	97,{
	show "[Turista]";
	if(obb_quest == 1 ) goto L_YES;
	if(obb_quest == 2 ) goto L_DONE;  

	show "Olá, desconhecido!";
	show "Você poderia me ajudar com um problema... Se me ajudar eu lhe darei algo valioso.";
	next;
	show "[Turista]";
	show "Eu sou Starving. Desde a manhã eu não tive uma maçã…";
	show "Eu lhe direi um segredo se me alimentar.";
	next;

L_MENU:
	menu "Você gosta de comer o que?",-,"Eu já estou com sua comida.",L_FOOD, "volto mais tarde.", L_END;

	show "[Turista]";
	show "Ah... Hoje eu gostaria de:";
	show "20 ^0000FFCarnes^000000";
	show "20 ^0000FFBiscoito de Gengibre^000000";
	next;
	goto L_MENU;

L_NO_MEAT:
	delitem 517,20;//Items: Carnes,
	show "Não tente me enganar, ainda falta carnes...";
	show "Por Favor, Traga-me as carnes...";
	close;

L_NO_COOK:
	delitem 538,20;//Items: Biscoito de Gengibre,
	show "Awww... Eu qero mais biscoitos!";
	show "Por Favor, Por Favor traga-me mais...";
	close;

L_FOOD:
	show "[Turista]";
	show "O Que?! Você trouxe todo o alimento?";
	show "Até o último!";
	next;
	show "[Turista]";
	if(countitem(517)<20) goto L_NO_MEAT;//Item: Carne
	delitem 517,20;//Items: Carne
	if(countitem(538)<20) goto L_NO_COOK;//Items: Biscoito de gengibre
	delitem 538,20;//Items: Biscoito de gengibre
	if(rand(10)<7) goto L_HUNGRY;
	show "Hum... estou cheio Agora.";
	show "Agora escute-me. É importante.";
	next;
	show "[Turista]";
	show "Um amigo meu que vive em Morroc, conhece um velho homem em Comodo…";
	next;
	show "[Turista]";
	show "Esse homem velho sabe um estranho segredo sobre as antigas caixas azuis.";
	set obb_quest, 1;
	close;

L_HUNGRY:
	show "Eu ainda estou com fome!";
	show "isso é tudo que você conseguiu?";
	close;

L_YES:
	show "OK, tenho que ir em Morroc e encontra o velho Jacob,";
	show "ajudar-lhe a seguir um homem velho, que possa manter um conhecimento como fazer algumas caixas azuis…";
	next;
	show "[Turista]";
	show "Boa sorte, meu amigo.";
	close;

L_DONE:
	show "Você já fêz alguma caixa azul?";
	next;
	show "[Turista]";
	show "Oh... Sim, você encontrou-se com esse velho grego de Comodo… ";
	close;

L_END:
	show "[Turista]";
	show "Tudo bem, mas não se esqueça.";
	close;
}

//second part Jacob
morocc.gat,267,142,5	script	Jacob	807,{
	show "[Jacob]";
	if(obb_quest == 1 ) goto L_YES;
	if(obb_quest == 2 ) goto L_DONE;
	show "Sai fora! Eu estou ocupado!";
	if(rand(2))show "Não é nenhum de seus negócios!";
	if(rand(2))show "Que Alberta? Huh?! Eu disse-o alto? Esqueça.";
	close;

L_YES:
	show " Aha! Você diz que conhece meu velho amigo de Alberta? ";
	next;
	show "[Jacob]";
	show "Bem, eu só digo sobre esse velho homem estranho se você me trouxer";
	show "2 ^0000FFSem Destinatário^000000";
	next;
	menu "Ésta aqui!",-,"Eu mudei de plano.",L_END;

	show "[Jacob]";
	show "Deixar-me ver…";
	if(countitem(636)<2) goto L_NOITM;//Items: Sem Destinatário
	delitem 636,2;//Items: Sem Destinatário
	show "Obrigado, isso é tudo que eu necessito.";
	emotion e_thx;
	next;
	show "[Jacob]";
	set obb_quest, 2;
L_DONE:
	show "Vá até Comodo. Em algum lugar na praia você encontrará o velho grego.";
	show "Diga-lhe que me conhece e ele ajudará.";
	close;

L_NOITM:
	show "Perdão, mas eu necessito realmente desses dois  ^0000FFSem Destinatários^000000.";
	emotion e_hmm;
	close;

L_END:
	show "[Jacob]";
	show "Bom, Boa sorte.";
	close;
}

//third part
comodo.gat,41,209,5	script	Velho Grego	120,{
	show "[Velho Grego]";
	if(obb_quest == 2 ) goto L_YES;
	if(obb_quest < 2 ) goto  L_LOST;

L_LOST:
	show "O CONHECIMENTO! Aquela é a chave, minha criança…";
	if(rand(2))show "A riqueza não importa.";
	close;

L_YES:
	show "Huh? Jacob lhe mandou vim aqui?";
	show "OK, OK... É verdade, eu ainda posso fazer velhas cópias azuis";
	show "as ^0000FF Velha Caixa Azul^000000";
	next;

L_MENU:
	menu "Fazer-me um.",L_CREATE,"O que você necessita?",-,"Adeus.",L_END;

	show "[Velho Grego]";
	show "Para uma Velha Caixa Azul eu necessito:";
	show "50 ^0000FFCyfars^000000";
	show "10 ^0000FFBrigans^000000";
	show "10 ^0000FFCascos de Ostras^000000";
	show "15 ^0000FFCascos de Caranguejos^000000";
	next;
	goto L_MENU;

L_CREATE:
	show "[Velho Grego]";
	show "Vamos ver… O que o têm ai…";
	next;
L_CREATE2:
	if(countitem(7053)<50 || countitem(7054)<10 || countitem(965)<10 || countitem(964)<15) goto L_NO;//Items: Cyfar, Brigan, Clam_Shell, Crab_Shell,
	delitem 7053,50;//Items: Cyfar
	delitem 7054,10;//Items: Brigan
	delitem 965,10;//Items: Casco de Ostra
	delitem 964,15;//Items: Casco de Caranguejo
	additem 603,1;//Items: Velha Caixa Azul
	show "[Velho Grego]";
	show "Está aqui sua caixa!";
	emotion e_no1;
	next;
	menu "Fazer Outra!",L_CREATE2, "Isso já é o bastante.",-;

L_END:
	show "[Velho Grego]";
	show "Eu desejo-lhe uma feliz viagem, minha criança.";
	close;

L_NO:
	show "[Velho Grego]";
	show "Desculpe, minha criança. Você não trouxe bastante materiais…";
	close;
}