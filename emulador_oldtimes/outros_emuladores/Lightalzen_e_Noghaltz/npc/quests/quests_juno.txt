//===== Cronus Script ========================================
//= Quest em Juno
//===== Por: =================================================
//= eAthena Dev Team
//===== Versão: ========================================
//= 1.4
//===== Descrição: ===========================================
//= Ajude os bros e pegue alguns rubis e ouros. Essa quest é
//= usado também na parte de Quest de Alquimista
//===== Changelog: ===========================================
//= 1.3 Npc Iniciado [eA Dev Team]
//= 1.4 Traduzido [Kamper]
//============================================================

yuno_in01.gat,103,158,4	script	Bain	98,{
	set @name$, "[Bain]";
OnStart:
	show @name$;
	if(bros_q == 1) goto L_Check;
	show "Nós estamos indo inventar uma boneca que possa falar e se comunicar! Bem, ao menos é o que estou tentando fazer....";
	show "A idéia veio de uma velha fofoca sobre uma boneca que agisse como uma pessoa real.";
	next;
	show @name$;
	show "Eu acredito que se alguém teve a habilidade tempos atrás de fazer uma boneca dessas, hoje isso deve ser definitivamente possível hoje.";
	next;
	show @name$;
	show "Infelizmente eu preciso das fontes e das peças apropriadas para realmente tentar construir uma boneca.";
	show "Embora eu possa comprar algumas das coisas que eu necessito, muitos destes items podem somente ser deixados por monstros......";
	next;
	show @name$;
	show "Eu sou muito fraco para derrotar esses monstros....... Como poderei continuar este projeto??.....";
	next;
	menu "Eu vou pegar estes itens para você.",-, "Boa sorte.",M_End;

		show @name$;
		show "Sério? Tal bondade é rara nestes tempos de hoje em dia. Nós seríamos gratos pelo seu trabalho.";
		show "Aqui estão os itens que precisamos.......";
		callsub sF_List;
		set bros_q, 1;
		close;

	M_End:
		show @name$;
		show "Nós precisaremos de muita sorte, se for tentarmos criar. Se você conhecer qualquer um que possa nos ajudar, por favor apresente-nos.";
		close;

L_Check:
	if((countitem(974) < 1) || (countitem(612) < 5) || (countitem(7068) < 5) || (countitem(1003) < 5) || (countitem(7043) < 5) || 
	(countitem(757) < 3) || (countitem(756) < 3) || (Zeny < 2000)) goto L_NotEnuf;
	show "Muito obrigado cara! Você encontrou todos os itens que eu precisava.  Eu não poderia ter completado este projeto sem sua ajuda.";
	next;
	show @name$;
	show "Ok, deixe-me tentar!";
	show "Espere um segundo!";
	next;
	show "-Vrrrmrmrmrmrmrmrrmrmrmmrmrmrmrmmm";
	next;
	show "-Pakakakakakakakakakakakakakak";
	next;
	show "-Shakakakakakakakakakakaakakak";
	next;
	show @name$;
	show "AHHH SUCESSO!!";
	show "Não sei se podemos fazer tudo mais, mas lhe garanto que criei";
	set @wowitem,rand(2);
	if(@wowitem == 0) show "5 Rubis!";
	if(@wowitem == 0) additem 723,5;
	if(@wowitem == 1) show "3 Ouros!";
	if(@wowitem == 1) additem 969,3;
	delitem 974,1;
	delitem 612,5;
	delitem 7068,5;
	delitem 1003,5;
	delitem 7043,5;
	delitem 757,3;
	delitem 756,3;
	set Zeny, Zeny-2000;
	if(ALCH_Q == 6) set ALCH_Q2, 2;	//para a Quest de Alquimista
	set bros_q, 0;
	next;
	show @name$;
	show "Se você encontrar mais partes, traga-os. Heheheheh.";
	next;
	show @name$;
	show "Obrigado por me ajudar.";
	show "Você tem minha eterna gratidão.";
	show "Eu desejo-lhe sorte em suas viajens!";
	close;

	L_NotEnuf:
		show "Caso você esqueça, estes são os itens que nós precisaremos. Eu sugiro que você escreva-os.....";
		callsub sF_List;
		close;

sF_List:
	next;
	show @name$;
	show "^5533FF1 Mistura";
	show "5 Mini-Fornalhas";
	show "5 Madeiras Queimadas";
	show "5 Carvões";
	show "5 Areias Finas";
	show "3 Minérios de Elunium";
	show "3 Minérios de Oridecon^000000";
	next;
	show @name$;
	show "Nós precisaremos também de ^5533FF2,000 zeny^000000.";
	show "O dinheiro ajudará a pagar pelas despesas menores envolvidas no empreendimento deste projeto. Boa sorte e tome cuidado.";
	return;
}

yuno_in01.gat,99,153,4	script	Bajin	47,{
	set @name$, "[Bajin]";
	doevent "Bain::OnStart";
	close;
}
