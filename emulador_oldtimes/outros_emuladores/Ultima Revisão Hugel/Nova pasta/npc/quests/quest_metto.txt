//===== Cronus Script ========================================
//= Quest do Metto
//===== Versão Atual: ========================================
//= 1.1/1.1
//===== Compatível Com: ======================================
//= Cronus
//===== Descrição: ===========================================
//= Quest do Metto
//===== Comentários Adicionais: ==============================
//= Separado do cidades/juno.txt
//= 1.1 Traduzido por [FrozenCirius]
//============================================================

yuno_in03.gat,25,39,3	script	Metto	709,{
	show "[Metto]";
	if(metto_q == 2 || metto_q == 4 || metto_q == 6) goto L_Check;
	if(metto_q == 1) goto L_Stang;
	if(metto_q == 3) goto L_Kato;
	if(metto_q == 5) goto L_Cici;
	if(metto_q == 7) goto L_Ruined;
	if(metto_q > 7) goto L_Done;
	show "Pode ser hoje, amanhã, ou depois de amanhã, Eu gasto cada momento que tenho em minha pesquisa.";
	show "Dediquei minha vida inteira para fazer uma............";
	next;
	show "[Metto]";
	show "NOVA JUNO, meu próprio reino.  Para tal eu preciso saber como essa cidade flutua no ar.";
	show "Se eu fosse capaz de fazer...meus objetivos não seriam meros sonhos, mas uma realidade!!!";
	next;
	show "[Metto]";
	show "Aquele ^5544FFWagan^000000 pensa que minha pesquisa não é nada...";
	show "Mas ele não percebe que minha pesquisa foi abençoada pelos próprios deuses!";
	close;

L_Stang:
	show "Vou pedir para meu colega ^5533FFStangckle^000000 me ajudar. Tenho certeza que ele ele ajudará de bom grado um velho amigo.";
	next;
	show "[Metto]";
	show "Hmmm.... Se eu tiver a ajuda do meu camarada será muito mais fácil... Ore! Ore para qualquer deus que você acredita para que dê certo!";
	set metto_q, 2;
	close;
L_Kato:
	show "O que??!! Aquele tolo do Stangckle disse que não quer me ajudar?? Isso não é bom... Minha pesquisa vai demorar a sair consideravelmente.";
	show "......... O que farei agora?!?!?";
	emotion e_swt2;
	next;
	show "[Metto]";
	show "Por sorte eu tenho um plano de reserva.  Estou certo de que meu amigo ^5533FFKato^000000 me dará uma mãozinha.";
	next;
	show "[Metto]";
	show "Se eu tivesse a chance de estudar o super robô dele, o 'Grande Z', Eu poderia continuar minha pesquisa...";
	next;
	show "[Metto]";
	show "Quero mais que aquele Stangckle se dane.  Mesmo que ele não queira ajudar, minha pesquisa pode continuar... deve continuar!";
	next;
	show "[Metto]";
	show "Pode esperar grandes coisas de mim jovem amigo... MUHAHAHAHAH!!!!";
	set metto_q, 4;
	emotion e_gg;
	close;
L_Cici:
	show "O QUE!?!? Você está dizendo que a coisa que mais preciso para fazer minha pesquisa, o Grande Z...foi DESTRUIDO!?!?";
	emotion e_wah;
	next;
	show "[Metto]";
	show "NÃÃÃÃÃOOOO!!! Por que?? Como?? Porque ele fez aquilo? Os deuses estão me amaldiçoando?!?";
	emotion e_an;
	next;
	show "[Metto]";
	show "Espere... Preciso me acalmar...";
	show "Se eu pudesse ter a ajuda do Professor ^5533FFCiCi^000000 com sua 'Teoria de Motor Giratório', Eu seria capaz de terminar minha pesquisa...";
	next;
	show "[Metto]";
	show "*sigggghhhh* Eu ainda tenho esperança... SIM, EU TENHO!! HAHAHA!!!";
	set metto_q, 6;
	emotion e_gg;
	close;
L_Check:
	show "É melhor eu ir indo para checar esse novo aparato... O que você ainda está fazendo aqui?";
	show "Vou sair por um momentinho.  Preciso me preparar, então saia daqui e continue seu caminho...";
	close;
L_Ruined:
	show "............................";
	next;
	show "[Metto]";
	show "Como algo assim pôde acontecer?? Todos os meus 3 colegas me abandonaram afinal?!?!";
	show "É verdade... os deuses me odeiam... Estou arruinado...ARRUINADO!!!";
	emotion e_sob;
	next;
	show "[Metto]";
	show "O que mais posso fazer agora? Será que chegou a hora de interromper meus estudos?....";
	show "Esses muitos, muitos anos de pesquisas?........";
	emotion e_sob;
	next;
	show "[Metto]";
	show "Você deve estar desapontado...... mas por favor, esqueça tudo isso... me esqueça também...";
	show "Quem fez isso pra mim? Que tipo de pessoa maligna iria destruir os sonhos de um homem??";
	next;
	show "[Metto]";
	show "De repente me senti cansado....... Chegou minha hora de desistir... Está tudo acabado.  Por favor não fique desapontado também...";
	set metto_q, 8;
	close;
L_Done:
	show "Talvez eu deva procurar um novo emprego.  Eu ouvi que há coisas populares para se afzer hoje em dia. Me pergunto qual profissão eu deveria seguir?";
	show "Tudo que sei é que não quero mais ser cientista.";
	next;
	show "[Metto]";
	show "Nunca mais me envolverei em pesquisas...";
	close;
}

//=====================================================================
yuno_in01.gat,18,95,5	script	Stangckle	99,{
	show "[Stangckle]";
	if(metto_q > 2) goto L_Done;
	if(metto_q == 2) goto L_Start;
	show "Eu sou um dos cientistas dessa cidade. Meu nome é ^5533FFStangckle^000000.";
	show "Lembre-se do meu nome, talvez seja útil no futuro. Quem sabe, talvez eu invente algo incrível.";
	close;
L_Start:
	show "O que posso fazer por você? Meu colega Metto virá aqui em breve, então eu não tenho muito.";
	show "Então o que você disse que precisa??";
	next;
	menu "Preciso falar com você sobre Metto!!",-, "Atualmente eu não preciso de nada.",M_End;

		show "[Stangckle]";
		show "......................";
		next;
		show "[Stangckle]";
		show "..................................";
		next;
		show "[Stangckle]";
		show "Isso é verdade?? Metto não me vê como um colega, e está apenas me USANDO?";
		show "Ele me vê apenas como um assistente e vai ROUBAR todo o CRÉDITO pelas nossas descobertas?!!";
		emotion e_omg;
		next;
		show "[Stangckle]";
		show "Não posso acreditar que confiei nele... os boatos eram verdadeiros... ele É um homem louco e egoísta!!!.......";
		show "E pensar que eu respeitei um boboca como aquele!!";
		emotion e_pif;
		next;
		show "[Stangckle]";
		show "Não acho que vou suportar ver aquela cara nojenta dele novamente...";
		next;
		show "[Stangckle]";
		show "Estou feliz que uma pessoa verdadeira e honesta como você teve coragem para me contar a verdade sobre Metto.";
		show "Obrigado por compartilhar essa informação comigo.";
		next;
		show "[Stangckle]";
		show "Agora, se você me permite, eu tenho trabalho á fazer.";
		set metto_q, 3;
		close;
	M_End:
		show "[Stangckle]";
		show "O que você está fazendo aqui então? Não me faça desperdiçar tempo.";
		emotion e_swt;
		close;
L_Done:
	show "Então Metto era verdadeiramente um cientista louco no fim das contas... Eu acho que ele é quem mais vai se prejudicar pela escolha que fez...";
	close;
}

//===================================================================
yuno_in01.gat,19,182,3	script	Kato	55,{
	show "[Kato]";
	if(metto_q > 4) goto L_Done;
	if(metto_q == 4) goto L_Start;
	show "Eu sou um cientista especializado em ^5533FFInteligência artificial e robótica^000000.";
	show "Porém não estou trabalhando em projeto algum por agora, minha última criação foi o 'Grande Z', sem dúvida um trabalho de gênio!";
	close;

L_Start:
	show "Como cientista, eu pesquiso sobre muitas coisas... é claro que as pesquisas são secretas. O que posso fazer por você?";
	next;
	menu "Eu tenho notícias de Metto!",-, "Só queria dizer olá.",M_End;

		show "[Kato]";
		show "O QUE!!?? Metto disse que o Grande Z é INFERIOR aos experimentos de Stangckle!!??";
		show "Ele... ele disse que eu devo mudar o nome do meu robô para 'Vergonhoso Z'??.......";
		emotion e_omg;
		next;
		show "[Kato]";
		show "*Sniff* Eu... Eu não acredito... *sob* como ele pôde dizer aquilo?..... se... se meu invento não é bom...";
		show "Então eu acho que devo destruí-lo... meu Grande Z.... *sigh*";
		emotion e_sob;
		next;
		show "[Kato]";
		show "Como aquele Metto de tornou um desgraçado sem coração?? Obrigado por me contar os VERDADEIROS sentimentos de Metto sobre meu trabalho.";
		set metto_q, 5;
		close;
	M_End:
		show "[Kato]";
		show "........... Ok.... Hum.... Olá...";
		close;

L_Done:
	show "Estou interessado em pesquisar inteligência artificial.  Você sabe, com robôs.";
	show "Eu sei que é algo bem difícil de se fazer, mas se tem um jeito, eu irei conseguir.";
	next;
	show "[Kato]";
	show "Se você ver algo incrível relacionado ao que eu descreví, você saberá que é obra minha.";
	close;
}

//===================================================================
yuno_in03.gat,179,43,5	script	CiCi	121,{
	show "[CiCi]";
	if(metto_q > 6) goto L_Done;
	show "Meu nome é ^5533FFCiCi^000000.  Eu sou um dos ^5533FF3 maiores cientistas^000000 que vivem nesta cidade.";
	show "Porém, existem muitos cientistas nessa cidade, ser um dos 3 melhores é uma verdadeira honra, você não acha??";
	if(metto_q == 6) goto L_Start;
	close;

L_Start:
	next;
	show "[CiCi]";
	show "Heh, Eu acho que estou sendo um pouco egoísta... de qualquer forma, o que posso fazer por você?";
	next;
	menu "Metto quer que eu lhe conte algo...",-, "Nada, apenas dizer oi.",M_End;

		show "[CiCi]";
		show "Hmm?? Metto disse que não precisa da minha Teoria de Motor Giratório?? Hmphf! Como ele ousa!!!";
		emotion e_pif;
		next;
		show "[CiCi]";
		show "Acho que eu vou ensinar á ele algumas coisas sobre a VERDADEIRA ciência e pesquisas INTELIGENTES!!";
		emotion e_ag;
		next;
		show "[CiCi]";
		show "... Mas... mesmo assim eu não tenho o equipamento necessário para ajudá-lo.";
		show "Eu acho que ele não tem chance alguma de conseguir o que quer.";
		next;
		show "[CiCi]";
		show "Em primeiro lugar, diga a ele que não quero fazer parte do experimento dele.";
		show "Ele não está fazendo algo que seja de utilidade pública...";
		set metto_q, 7;
		close;

	M_End:
		show "[CiCi]";
		show "O que? É isso? Você só queria gastar meu tempo?...........";
		emotion e_swt;
		close;

L_Done:
	show "Hmm... Eu quero melhorar o Motor Giratório, mas não tenho as peças necessárias.  Que dor de cabeça...";
	show "Metto nunca me ofereceu ajuda alguma.  Isso não será fácil...";
	close;
}

//===================================================================
yuno_in01.gat,18,30,3	script	Wagan	85,{
	show "[Wagan]";
	if(metto_q > 0) goto L_Check;
	show "Estou certo de que nossa vila é capaz de prosperar por causa da brilhante pesquisa realizada aqui.";
	show "Nós devemos muito áquele que dedicaram suas vidas aos estudos e pesquisas.";
	next;
	show "[Wagan]";
	show "É por causa deles que hoje nós podemos aproveitar de uma excelente qualidade de vida aqui.";
	show "Eles foram capazes de ajudar uns aos outros, o que é ótimo de se ver.";
	next;
	show "[Wagan]";
	show "Infelizmente, existem algumas 'laranjas podres', que causam problmeas nessa sólida comunidade de pesquisadores.";
	next;
	show "[Wagan]";
	show "Essas pessoas me deixam louco!";
	emotion e_an;
	next;
	show "[Wagan]";
	show "Eles só pensam em sí mesmos e em suas ações loucas, tornam tudo difícil para todos.";
	show "De todos esses homens egoístas, ^FF3333'Metto'^000000 deve ser, de longe, o pior.";
	next;
	show "[Wagan]";
	show "Esse indivíduo perturbado não faz nada, a não ser destruir facilidades públicas, criar coisas exageradamente poluentes, perturbar a vida dos outros...";
	next;
	show "[Wagan]";
	show "...e a lista ainda vai longe.  E o que é pior..... Ele tenta justificar suas terríveis ações, dizendo que são 'pesquisas'!!!!";
	show "Ele definitivamente perdeu sua sanidade!!";
	emotion e_ag;
	next;
	show "[Wagan]";
	show "Se tivesse um jeito de impedir esse homem louco de destruir nossa comunidade...";
	show "Queria que existisse alguém capaz e bravo o suficiente para curar a insanidade de ^FF3333Metto's^000000...";
	next;
	menu "Deixa comigo.",-, "Desejo sorte para sua cidade...",M_End;

		show "[Wagan]";
		show "Oh?? Você realmente vai nos ajudar á parar Metto? Nossa cidade ficará muito grata se você conseguir...";
		show "Mas como, exatamente, você pretende por seu plano em prática?";
		emotion e_what;
		next;
		menu "É, realamente será difícil...",M_End, "Bem, Acho que preciso pensar em um plano agora...",-;

			show "[Wagan]";
			show "Sim, um plano sólido será necessário para parar Metto. Precisaremos de uma boa artimanha se quisermos parar aquele cara...";
			show "Tem que ser algo bem bolado, como... hmm........";
			next;
			show "[Wagan]";
			show "... Sabotar seus experimentos de forma que ele pense quer está destinado á falhar.";
			show "Ele é bem falante, então você conseguirá saber de forma fácil tudo que precisa saber sobre seus inventos.";
			next;
			show "[Wagan]";
			show "Para ser honesto.... Pensarei mais sobre esse plano por um tempo...";
			set metto_q, 1;
			close;
	M_End:
		show "[Wagan]";
		show "Temos a esperança de que a fé estará do nosso lado enquanto esperamos as coisas acontecerem...";
		show "Pense nisso... Metto acredita fortemente em sua fé... Se nós pudessemos usar isso de alguma forma para ganhar vantagem...";
		close;
L_Check:
	if(metto_q == 8) goto L_Thank;
	if(metto_q == 9) goto L_Done;
	show "Como vai seu trabalho de sabotar os experimentos de Metto?  Espero que esteja indo muito bem.";
	show "Me desculpe por te envolver nos problemas de nossa cidade, mas nós estamos apreciando seu esforço.";
	close;
L_Thank:
	show "É verdade? Você foi capaz de parar os experimentos malucos de Metto? Muito obrigado por sua ajuda.";
	show "Sem Metto causando problemas, temos muito menos coisas para nos preocupar.";
	emotion e_ic;
	next;
	show "[Wagan]";
	show "O povo dessa cidade é grato á você. Aqui está uma pequena demonstração de nossa gratidão...";
	next;
	show "[Wagan]";
	show "Obrigado mais uma vez por sua assistência! Eu lhe desejo toda a sorte em seu futuro!!!";
	additem 715,10;
	emotion e_thx;
	set metto_q, 9;
	close;
L_Done:
	show "As coisas estão muito mais calmas por aqui, agora que Metto parou com suas experiências malucas. Obrigado mais uma vez por sua ajuda.";
	emotion e_thx;
	close;
}