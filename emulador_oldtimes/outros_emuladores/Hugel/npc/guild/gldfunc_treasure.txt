//===== Cronus Script ========================================
//= Guerra de Emperium Funções do Quarto do Tesouro
//===== Por: =================================================
//= jAthena - kalen (1.0)
//= 1.1 por Akaru, ho|yAnge|X
//===== Versão Atual: ========================================
//= 1.8/1.8
//==============================================
//= Break down of arguments used in the F_GldTreas:
//=   arg(0): name of guild castle
//=   arg(1): name of script that called the function
//=   arg(2): the box number amount
//=   arg(3): temp variable (count)
//=   arg(4): temp variable (box/monster id#)
//=   arg(5): box/monster id#
//=   arg(6): x1 coordinate for areamonster call
//=   arg(7): y1 coordinate for areamonster call
//=   arg(8): x2 coordinate for areamonster call
//=   arg(9): y1 coordinate for areamonster call
//=   
//= Break down of arguments used in the F_GldTreasSw:
//=   arg(0): name of guild castle.
//=   arg(1): x1 coordinate for warp back to guild castle
//=   arg(2): y1 coordinate for warp back to guild castle
//===== Comentários Adicionais: ==============================
//= 1.8 Traduzido [emeeca]
//============================================================

//================================================
// Treasure Spawning Function
//================================================
function	script	F_GldTreas	{
	if(getarg(10) == 1) goto L_SPAWN;
	setcastledata getarg(0)+".gat",4,0;
	setcastledata getarg(0)+".gat",5,0;
	killmonster getarg(0)+".gat","Treasure_"+getarg(1)+"::OnDied";
	if(GetCastleData(getarg(0)+".gat",2) > 100 || GetCastleData(getarg(0)+".gat",1) == 0) return;
	//sets the counter variable = to the box number amount
	if(getarg(0) == "nguild_prt" || getarg(0) == "nguild_alde" || getarg(0) == "nguild_gef" || getarg(0) == "nguild_pay" ) {
		//Novice Castles can't have more than 1 Treasure Chest
		set getarg(2),1;
	}else{
		set getarg(2),GetCastleData(getarg(0)+".gat",2)/5+4;
	}
	if (getarg(2) <= 0) return;
	set getarg(3), getarg(2);	//sets the counter variable = to the box number amount

L_SPAWN:
	set getarg(4), getarg(5)+((getarg(3)+1) & 1);	//sets the box id variable = to the box id
	areamonster getarg(0)+".gat",getarg(6),getarg(7),getarg(8),getarg(9),"Treasure Chest",getarg(4),1,"Treasure_"+getarg(1)+"::OnDied";
	set getarg(3), getarg(3) - 1;
	if(getarg(3) > 0) goto L_SPAWN;
	return;
}

//==============================================================
//  Treasure Room Switch 
//===============================================================
function	script	F_GldTreasSw	{
	show " ";
	show "Há uma pequena lavanca por aqui.";
	show "Você gostaria de puxa-la para baixo?";
	next;
	menu "Sim",M_1,"Não",-;
	close;
	
	M_1:
		warp getarg(0)+".gat",getarg(1),getarg(2);
		return;
}
