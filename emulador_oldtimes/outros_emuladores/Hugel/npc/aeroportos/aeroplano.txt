//===== Cronus Script ========================================
//= Sistema de Aeronave
//===== Por: ================================================= 
//= MasterOfMuppets
//===== Versão Atual: ======================================== 
//= 0.9/0.9
//===== Comentários Adicionais: ============================== 
//= 0.6a profPardal [pucRO]
//= * Trechos não traduzidos possuem a tag [NT] para fácil
//=   localização e correção.
//= * Noshow de NPCs não foram traduzidos
//= * O script não foi adaptado para o Cronus. Não sei todos
//=   os comandos que precisam de adaptação, então preferi
//=   manter todos originais.
//= 0.7a Termianda a Tradução [Pudim]
//= 0.8 Atualizado para Hugel [Page]
//= 0.9 Atualizado e corrigido erros [Kamper]
//============================================================ 

//============================================================ 
//= Sistema de Aeroplano (Juno -> Hugel -> Einbroch -> Lighthalzen -> repete)
//============================================================ 

airplane,243,74,4	script	#AirshipWarp-1	45,2,2{
OnTouch:
	if($@airplanelocation == 1)warp "einbroch",90,275;
	if($@airplanelocation == 2)warp "yuno",85,265;
	if($@airplanelocation == 3)warp "lighthalzen",302,75;
	if($@airplanelocation == 4)warp "hugel",182,150;
	end;

OnHide:
	misceffect 16;
	end;
OnUnhide:
	misceffect 215;
	end;
}

airplane,243,29,4	script	#AirshipWarp-2	45,2,2{
OnTouch:
	if($@airplanelocation == 1)warp "einbroch",90,275;
	if($@airplanelocation == 2)warp "yuno",85,265;
	if($@airplanelocation == 3)warp "lighthalzen",302,75;
	if($@airplanelocation == 4)warp "hugel",182,150;
	end;

OnHide:
	misceffect 16;
	end;
OnUnhide:
	misceffect 215;
	end;
}

airplane,1,1,0	script	EinYuno_Airship	-1,{
OnInit:
while(TRUE)
{
	initnpctimer;
	setnpctimer 0;
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	disablenpc "#AirshipWarp-1";
	disablenpc "#AirshipWarp-2";
	mapannounce "airplane","O Aeroplano está levantando vôo. Nosso próximo destino é Hugel.",1,0xBA55D3;
	end;
OnTimer15000:
	mapannounce "airplane","Nós estamos a caminho de Hugel.",1,0xBA55D3;
	end;
OnTimer30000:
	mapannounce "airplane","Vamos chegar à Hugel em breve.",1,0xBA55D3;
	end;
OnTimer45000:
	set $@airplanelocation,4;
	enablenpc "#AirshipWarp-1";
	enablenpc "#AirshipWarp-2";
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";	
	mapannounce "airplane","Bem vindos à Hugel. Desejamos um bom passeio a todos.",1,0xBA55D3;
	end;
OnTimer55000:
	mapannounce "airplane","Estamos em Hugel atualmente. O avião levantará vôo em alguns minutos.",1,0xBA55D3;
	end;
OnTimer65000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	disablenpc "#AirshipWarp-1";
	disablenpc "#AirshipWarp-2";
	mapannounce "airplane","O Aeroplano está levantando vôo. Nosso próximo destino é Einbroch.",1,0x00FF00;
	end;
OnTimer80000:
	mapannounce "airplane","Nós estamos a caminho de Einbroch.",1,0x00FF00;
	end;
OnTimer95000:
	mapannounce "airplane","Vamos chegar à Einbroch em breve.",1,0x00FF00;
	end;
OnTimer110000:
	set $@airplanelocation,1;
	enablenpc "#AirshipWarp-1";
	enablenpc "#AirshipWarp-2";
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem vindos à Einbroch. Desejamos um bom passeio a todos.",1,0x00FF00;
	end;
OnTimer120000:
	mapannounce "airplane","Estamos em Einbroch atualmente. O avião levantará vôo em alguns minutos.",1,0x00FF00;
	end;
OnTimer130000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	disablenpc "#AirshipWarp-1";
	disablenpc "#AirshipWarp-2";
	mapannounce "airplane","O Aeroplano está levantando vôo. Nosso próximo destino é Lighthalzen.",1,0xFF8600;
	end;
OnTimer145000:
	mapannounce "airplane","Nós estamos a caminho de Lighthalzen.",1,0xFF8600;
	end;
OnTimer160000:
	mapannounce "airplane","Vamos chegar à Lighthalzen em breve.",1,0xFF8600;
	end;
OnTimer175000:
	set $@airplanelocation,3;
	enablenpc "#AirshipWarp-1";
	enablenpc "#AirshipWarp-2";
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem vindos à Lighthalzen. Desejamos um bom passeio a todos.",1,0xFF8600;
	end;
OnTimer185000:
	mapannounce "airplane","Estamos em Lighthalzen atualmente. O avião levantará vôo em alguns minutos.",1,0xFF8600;
	end;
OnTimer195000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	disablenpc "#AirshipWarp-1";
	disablenpc "#AirshipWarp-2";
	mapannounce "airplane","O Aeroplano está levantando vôo. Nosso próximo destino é Juno.",1,0x70DBDB;
	end;
OnTimer210000:
	mapannounce "airplane","Nós estamos a caminho de Juno.",1,0x70DBDB;
	end;
OnTimer225000:
	mapannounce "airplane","Vamos chegar à Juno em breve.",1,0x70DBDB;
	end;
OnTimer240000:
	set $@airplanelocation,2;
	enablenpc "#AirshipWarp-1";
	enablenpc "#AirshipWarp-2";
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem vindos à Juno. Desejamos um bom passeio a todos.",1,0x70DBDB;
	end;
OnTimer250000:
	mapannounce "airplane","Estamos em Juno atualmente. O avião levantará vôo em alguns minutos.",1,0x70DBDB;
	end;
OnTimer260000:
	stoptimer;
}
}

//C4644E

//============================================================ 
//= Some normal NPCS (airplane)
//============================================================ 

airplane,240,64,5	script	Saída#01::Exit2	857,{

end;

}

airplane,247,64,5	duplicate(Exit2)	Saída#02	857

airplane,240,39,1	duplicate(Exit2)	Saída#03	857

airplane,247,39,1	duplicate(Exit2)	Saída#04	857

airplane,100,69,2	script	Tripulação do Aeroplano#01::Airship Crew	852,{

	show "[Tripulação do Aeroplano]";
	show "Se você chegou ao seu";
	show "destino e gostaria de";
	show "descer do avião, por";
	show "favor use as escadas";
	show "bem ali. Obrigado pela";
	show "preferência.";
	close;

}

airplane,250,59,3	script	Equipe do Aeroplano::AirshipInfo	67,{

	show "[Equipe do Aeroplano]";
	show "Bem vindo";
	show "a bordo.";
	show "Como posso ajudá-lo?";
	next;
	menu "Usando o Aeroplano",s_Use,"Cabine do Capitão",s_Capt,"Facilidades",s_Faci,"Cancelar",-;

	show "[Equipe do Aeroplano]";
	show "Bem, espero que você";
	show "aprecie voar";
	show "em nossos Aeroplanos.";
	show "Obrigado e tenha um bom dia.";
	close;

s_Use:
	show "[Equipe do Aeroplano]";
	show "Quando você ver uma";
	show "mensagem anunciando";
	show "que nós chegamos ao";
	show "seu destino, por favor";
	show "use uma das saídas";
	show "localizadas ao Norte";
	show "e ao Sul do avião.";
	next;
	show "[Equipe do Aeroplano]";
	show "Não se preocupe se por";
	show "acaso você perder seu";
	show "ponto. O avião está";
	show "constantemente em rota";
	show "e você terá outra chance";
	show "de descer em seu destino.";
	close;

s_Capt:
	show "[Equipe do Aeroplano]";
	show "A cabine do Capitão está";
	show "localilzada na parte";
	show "frontal do Aeroplano. Lá você";
	show "pode conhecer o capitão e";
	show "o piloto.";
	close;

s_Faci:
	show "[Equipe do Aeroplano]";
	show "O avião dispõe de vários";
	show "Mini Jogos para o";
	show "entretenimento de todos";
	show "os nossos passageiros.";
	show "Você está convidado a";
	show "testar suas habilidades";
	show "e sorte em nossos Mini Jogos~";
	close;

}

airplane,50,66,4	script	Meltz::AirshipApples	86,{

	show "[Meltz]";
	show "Bem vindo a Loja do Meltz, onde você pode";
	show "comprar Maçãs ou";
	show "espremê-las para fazer";
	show "Suco de Maçã.";
	next;
	menu "Comprar Maçãs",s_Buy,"Fazer Suco de Maçã",s_Juice,"Cancelar.",-;

s_Cancel:
	show "[Meltz]";
	show "Obrigado por parar em";
	show "minha loja. Boa viagem!";
	show "Volte sempre que desejar";
	show "comer uma maçãzinha~";
	close;

s_Buy:
	show "[Meltz]";
	show "Por favor entre com a";
	show "quantidade de maçãs que";
	show "você quer comprar. Cada";
	show "maçã custa ^0000ff15z^000000 e você pode";
	show "comprar no máximo 500 por";
	show "vez. Entre com \"0\"para";
	show "cancelar sua compra.";
	next;
	digit @tempapple;
	if(!@tempapple)goto s_Cancel;
	if(@tempapple > 0 && @tempapple < 501)goto s_AppleBuy;
	if(@tempapple < 0)close;
	show "[Meltz]";
	show "A quantidade máxima de maçãs";
	show "que você pode comprar por vez";
	show "é 500. " + @tempapple + " é";
	show "certamente maior que 500.";
	close;

s_AppleBuy:
	show "[Meltz]";
	show "Um total de ^FF0000" + @tempapple + "^000000 Maçãs";
	show "vão te custar ^FF0000" + @tempapple * 15 + "^000000 zeny.";
	show "Deseja continuar?";
	next;
	menu "Sim",-,"Não",s_Cancel;

	if(zeny < (@tempapple * 15))goto s_NEnoughZeny;
	show "[Meltz]";
	show "Obrigado por parar em";
	show "minha loja. Espero que";
	show "você aprecie o sabor das";
	show "Maçãs~";
	additem 512,@tempapple;
	set zeny,zeny - (@tempapple * 15);
	close;

s_NEnoughZeny:
	show "[Meltz]";
	show "Desculpe mas parece que";
	show "você não tem Zeny suficiente.";
	show "Volte sempre quando";
	show "quiser comer uma maçãzinha.";
	close;

s_Juice:
	show "[Meltz]";
	show "Ok, vou precisar de ";
	show "^FF00003 Maçãs e 1 Garrafa Vazia^000000";
	show "para fazer um Suco de Maçã.";
	show "Gostaria de continuar?";
	next;
	menu "Sim",-,"Não",s_Cancel;

	if(countitem(512) < 3 || !countitem(713))goto s_Material;
	show "[Meltz]";
	show "Obrigado,";
	show "por favor aguarde um";
	show "momento.";
	next;
	show "*Grind grind*";
	show "*Grind grind*";
	show "*Clang...!*";
	next;
	show "[Meltz]";
	show "Aqui está~";
	show "Espero que goste!";
	show "Sinta-se a vontade para";
	show "voltar sempre que quiser";
	show "uma Maçã ou Suco de Maçãs,";
	show "aventureiro~";
	delitem 512,3;
	delitem 713,1;
	additem 531,1;
	close;

s_Material:
	show "[Meltz]";
	show "Sinto muito, mas você não";
	show "tem todos os itens necessários";
	show "para fazer o Suco de Maçã.";
	show "Lembre-se, eu preciso de";
	show "3 Maçãs e 1 Garrafa Vazia";
	show "para cada suco.";
	close;

}


airplane,80,71,4	script	Zerta	834,{
	show "[Zerta]";
	show "Oh, olá Aventureiro.";
	show "Estou atualmente em uma";
	show "Jornada Santa, orando";
	show "em propósito do continente";
	show "de Rune-Midgard.";
	close;
}

airplane,65,63,4	script	Maelin	714,{
	show "[Maelin]";
	show "Humm, este avião vai para";
	show "Lutie, não? Estou";
	show "esperando há muito tempo,";
	show "mas até agora nada de";
	show "anúncio sobre Lutie.";
	close;
}

airplane,72,34,2	script	Aanos	702,{
	show "[Aanos]";
	show "Oh uaauu~";
	show "O céu tem uma aparência";
	show "tão diferente e";
	show "adorável, visto daqui de cima!";
	close;
}

airplane,32,61,4	script	Nils::TypingContest	49,{
	callfunc "F_ClearGarbage"; //Clear outdated, unused variables
loopback:
	show "[Nils]";
	show "Bem vindo ao ^FF0000Desafio";
	show "Ragnarök de Digitação^000000.";
	show "Gostaria de jogar um jogo";
	show "de velocidade em digitação?";
	next;
	menu "Jogar o ^FF0000DRD^000000",s_Play,"Informações",s_Info,"Recordes",s_Record,"Cancelar",-;

	show "[Nils]";
	show "Sinta-se a vontade para";
	show "jogar a qualquer momento.";
	show "Eu estarei por aqui~";
	close;

s_Play:
	show "[Nils]";
	show "Ok, temos um novo";
	show "desafiante!";
	show "Digite o texto a seguir";
	show "o mais rápido que você";
	show "puder, sem nenhum erro!";
	show "Vamos começar~!";
	next;

	//below arrays are for simplified entering of new lines ;P
	//first lines array
	setarray $@textstringsa$[0],"^00FFFFthkelfkskeldmsiejdlsle^0000FFhfndkelsheidl",
	"^993366hfjdkeldjsieldjs^663366hfjdjeiskdlefvbd",
	"^00FFFFCoboman no chikara-yumei na",
	"^993366belief love luck grimace sweat rush",
	"^00FFFFcallipygian salacious lascivious",
	"^663366uNflAPPaBLe LoVaBLe SeCreTs AnD",
	"^00CCFFburrdingdingdilidingdingphoohudaamb",
	"^FF33CCbarapaphurarlandreamduranbatuhi^990066wooi",
	"^FF33CCLiGhTsPeEd RiGhT SPEed leFT TURn",
	"^00CCFFI'm the King of all Weirdos! Now",
	"^00CCFFBy the power of^000000",
	"^00CCFF...silence. quiet benevolence...";
	//second lines array
	setarray $@textstringsb$[0],"skemd",
	"",
	"chikara-dalookii na chikara da ze!",
	"folktale rodimus optimus bumblebee",
	"licentious prurient concupiscent",
	"BoWLiNg aGaINST tHe KarMA of YoUtH",
	"andoora^0000FFbambarambambamburanbamding",
	"kabamturubamdingding",
	"RiGhT BuRn OrIGInAL GaNgSteR SmACk",
	"you know of my true power. Obey~!",
	"p-po-poi-po-poi-poin-poing",
	"soul mate... wonder. enigma...";
	//for the few 3-liners... =X
	setarray $@textstringsc$[0],"",
	"",
	"COBO ON!",
	"",
	"",
	"",
	"",
	"",
	"",
	"",
	"GOD-POING. I NEVER LOSE!",
	"cloud.";
	//comparisation strings
	setarray $@compstring$[0],"thkelfkskeldmsiejdlslehfndkelsheidlskemd",
	"hfjdkeldjsieldjshfjdjeiskdlefvbd",
	"Coboman no chikara-yumei na chikara-dalookii na chikara da ze! COBO ON",
	"belief love luck grimace sweat rush folktale rodimus optimus bumblebee",
	"callipygian salacious lascivious licentious prurient concupiscent",
	"uNflAPPaBLe LoVaBLe SeCreTs AnD BoWLiNg aGaINST tHe KarMA of YoUtH",
	"burrdindingdilidingdingphoohudaambandoorabambarambambamburanbamding",
	"barapaphurarlandreamduranbatuhiwooikabamturubamdingding",
	"LiGhTsPeEd RiGhT SPEed leFT TURn RiGhT BuRn OrIGInAL GaNgSteR SmACk",
	"I'm the King of all Weirdos! Now you know of my true power. Obey~!",
	"By the power of p-po-poi-po-poi-poin-poing GOD-POING. I NEVER LOSE!",
	"...silence. quiet benevolence... soul mate... wonder. enigma... cloud";
	//lengths
	setarray $@lengths[0],20,32,73,73,65,66,67,55,67,66,67,69;
	//	initnpctimer;
	show "[Nils]";
	set @index,rand(1,getarraysize($@textstringsa$))-1;
	set @string1a$,$@textstringsa$[@index];
	set @string1b$,$@textstringsb$[@index];
	set @string1c$,$@textstringsc$[@index];
	set @typingstring1$,$@compstring$[@index];
	set @typingkeys,$@lengths[@index];
	set @index2,rand(1,getarraysize($@textstringsa$))-1;
	while (@index2 == @index) {
		set @index2,rand(1,getarraysize($@textstringsa$))-1;
	}
	set @string2a$,$@textstringsa$[@index2];
	set @string2b$,$@textstringsb$[@index2];
	set @string2c$,$@textstringsc$[@index2];
	set @typingstring2$,$@compstring$[@index2];
	set @typingkeys,@typingkeys+$@lengths[@index2];
	set @start,gettime(3)*60*60+gettime(2)*60+gettime(1);
	show @string1a$;
	if(@string1b$ != "")show @string1b$;
	if(@string1c$ != "")show @string1c$;
	digit @comparisonvar$[1];
	next;
	show "^000000[Nils]";
	show @string2a$;
	if(@string2b$ != "")show @string2b$;
	if(@string2c$ != "")show @string2c$;
	digit @comparisonvar$[2];
	if(@comparisonvar$[1] == @typingstring1$ && @comparisonvar$[2] == @typingstring2$)set @typingcorrect,1;
	next;
	if(!@typingcorrect)goto s_Fail;
	set @time,(gettime(3)*60*60+gettime(2)*60+gettime(1))-@start; //seconds taken
	set @mypoints,(@typingkeys * 100) / @time;
	show "[Nils]";
	show "Você levou ^FF0000" + @time + " segs^000000";
	show "para digitar tudo e";
	show "sua pontuação final é de";
	show "^0000FF" + @mypoints + "^000000 pontos.";
	next;
	if(@mypoints <= $TypingRecord)goto loopback;
	show "[Nils]";
	show "Meus Parabéns!";
	show "Temos um novo recorde aqui.";
	show "Vou colocar seu nome no";
	show "topo da lista dos recordes imediatamente.";
	set $TypingRecord,@mypoints;
	set $TypingRecord$,strcharinfo(0);
	close;

s_Fail:
	show "^000000[Nils]";
	show "Sinto muito mas você não";
	show "digitou tudo corretamente.";
	show "Mas lembre-se, a prática";
	show "leva à perfeição!";
	close;

s_Info:
	show "[Nils]";
	show "O Desafio Ragnarök de Digitação";
	show "é um jogo onde você precisa";
	show "digitar o texto proposto no";
	show "menor tempo possível. O nome";
	show "dos melhores jogadores ficará";
	show "gravado para a posteridade.";
	show "Quer fama? Eis sua chance!";
	next;
	show "[Nils]";
	show "Gostaria de reforçar que";
	show "você precisa digitar todo";
	show "o texto que você ver em uma";
	show "única linha. Portanto ^ff0000não";
	show "pressione ENTER^000000, apenas";
	show "clique em 'OK'.";
	close;

s_Record:
	show "[Nils]";
	show "^0000FF" + $TypingRecord$ + "^000000";
	show "é o atual recordista,";
	show "com um total de";
	show "^0000FF" + $TypingRecord + "^000000 pontos.";
	show "Tente superá-lo da";
	show "próxima vez~";
	close;

}

airplane_01,221,158,6	script	Piloto#air-0::Pilot	852,{
	set @pilot, rand(1,4);
	if (@pilot == 1) {
		show "[Piloto]";
		show "Longitude, 131 graus leste.";
		show "Latitude, 37 graus norte.";
		show "Estamos no curso correto, capitão.";
		close;
	} else if(@aa == 2) {
		show "[Piloto]";
		show "Olhe como o dia está";
		show "nublado. É sempre difícil";
		show "navegar nos céus quando eles";
		show "não estão limpos. Acredito que";
		show "precisemos usar o radar.";
		close;
	} else if(@aa == 3) {
		show "[Piloto]";
		show "O capitão é um bom";
		show "homem e eu não consigo pensar";
		show "em uma pessoa melhor para comandar";
		show "este Aeroplano. Além de ser rígido";
		show "é um grande piloto.";
		next;
		show "[^ff0000Tarlock^000000]";
		show "^ff0000Ei...!^000000";
		show "^ff0000Menos conversa^000000";
		show "^ff0000e mais pilotagem!^000000";
		next;
		show "[Piloto]";
		show "Sim, senhor!";
		show "(Vê o que eu significo?)";
		close;
	}
		show "[Piloto]";
		show "Este uniforme é";
		show "realmente agradável, mas";
		show "é grosso demais para";
		show "usar em um Aeroplano.";
		next;
		show "[Piloto]";
		show "...";
		show "......";
		show "Quase ninguém vem a esta sala.";
		show "E o capitão é um troxa.";
		show "Tenho vontade de socar ele.";
		next;
		//Emotion "Pilot#airplane_01" ET_HUK
		show "[Piloto]";
		show "Ah...! Olá, Veja!";
		show "Está gostando do vôo?";
		close;
}

//============================================================ 
//= Sistema de Aeroplano (Izlude -> Juno -> Rachel -> repete)
//============================================================ 

airplane_01,243,74,4	script	#AirshipWarp-3	45,2,2{
OnTouch:
	if($@airplanelocation2 == 1)warp "izlude",202,56;
	if($@airplanelocation2 == 2)warp "yuno",20,265;
	//if($@airplanelocation2 == 3)warp "ra_fild12",292,204;
	end;

OnHide:
	misceffect 16;
	end;
OnUnhide:
	misceffect 215;
	end;
}

airplane_01,243,29,4	script	#AirshipWarp-4	45,2,2{
OnTouch:
	if($@airplanelocation2 == 1)warp "izlude",202,56;
	if($@airplanelocation2 == 2)warp "yuno",20,265;
	//if($@airplanelocation2 == 3)warp "ra_fild12",292,204;
	end;

OnHide:
	misceffect 16;
	end;
OnUnhide:
	misceffect 215;
	end;
}

airplane_01,1,1,0	script	YunoIzl_Airship	-1,{
OnInit:
while(TRUE)
{
	disablenpc "Quest_Izl";
	initnpctimer;
	setnpctimer 0;
	set $@airplanelocation2,0;
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	disablenpc "#AirshipWarp-3";
	disablenpc "#AirshipWarp-4";
	mapannounce "airplane_01","O Aeroplano está levantando vôo. Nosso próximo destino é Izlude.",1,0x00FF00;
	end;
OnTimer15000:
	set $@mobinv, $@mob_inv+1;
	set $@mobrand, rand(1,3);
	if($@mobinv >= 9 && $@mobrand == 3) {
		stopnpctimer;
		enablenpc "Quest_Izl";
		donpcevent "Quest_Izl::OnInvasion";
	}
	mapannounce "airplane_01","Nós estamos a caminho de Izlude.",1,0x00FF00;
	end;
OnTimer30000:
	mapannounce "airplane_01","Vamos chegar à Izlude em breve.",1,0x00FF00;
	end;
OnTimer45000:
	set $@airplanelocation2,1;
	enablenpc "#AirshipWarp-3";
	enablenpc "#AirshipWarp-4";
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";	
	mapannounce "airplane_01","Bem vindos à Izlude. Desejamos um bom passeio a todos.",1,0x00FF00;
	end;
OnTimer55000:
	mapannounce "airplane_01","Estamos em Izlude atualmente. O avião levantará vôo em alguns minutos.",1,0x00FF00;
	end;
OnTimer65000:
	set $@airplanelocation2,0;
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	disablenpc "#AirshipWarp-3";
	disablenpc "#AirshipWarp-4";
	mapannounce "airplane_01","O Aeroplano está levantando vôo. Nosso próximo destino é Juno.",1,0x70DBDB;
	end;
OnTimer80000:
	mapannounce "airplane_01","Nós estamos a caminho de Juno.",1,0x70DBDB;
	end;
OnTimer95000:
	mapannounce "airplane_01","Vamos chegar à Juno em breve.",1,0x70DBDB;
	end;
OnTimer110000:
	set $@airplanelocation2,2;
	enablenpc "#AirshipWarp-3";
	enablenpc "#AirshipWarp-4";
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Bem vindos à Juno. Desejamos um bom passeio a todos.",1,0x70DBDB;
	end;
OnTimer120000:
	mapannounce "airplane_01","Estamos em Juno atualmente. O avião levantará vôo em alguns minutos.",1,0x70DBDB;
	end;
OnTimer130000:
//	set $@airplanelocation2,0;
//	donpcevent "#AirshipWarp-3::OnHide";
//	donpcevent "#AirshipWarp-4::OnHide";
//	disablenpc "#AirshipWarp-3";
//	disablenpc "#AirshipWarp-4";
//	mapannounce "airplane_01","O Aeroplano está levantando vôo. Nosso próximo destino é Rachel.",1,0xFF8600;
//	end;
//OnTimer145000:
//	mapannounce "airplane_01","Nós estamos a caminho de Rachel.",1,0xFF8600;
//	end;
//OnTimer160000:
//	mapannounce "airplane_01","Vamos chegar à Rachel em breve.",1,0xFF8600;
//	end;
//OnTimer175000:
//	set $@airplanelocation2,3;
//	enablenpc "#AirshipWarp-3";
//	enablenpc "#AirshipWarp-4";
//	donpcevent "#AirshipWarp-3::OnUnhide";
//	donpcevent "#AirshipWarp-4::OnUnhide";
//	mapannounce "airplane_01","Bem vindos à Rachel. Desejamos um bom passeio a todos.",1,0xFF8600;
//	end;
//OnTimer185000:
//	mapannounce "airplane_01","Estamos em Rachel atualmente. O avião levantará vôo em alguns minutos.",1,0xFF8600;
//	end;
//OnTimer195000:
	stoptimer;
}
OnReturn:
	killmonsterall "airplane_01";
	set $@mobinv, 0;
	mapannounce "airplane_01","Ameaça de monstros terminada. O Aeroplano está voltando a operar normalmente.",1,0x00FF00;
	startnpctimer;
	end;
}

//============================================================ 
//= Some normal NPCS (airplane_01)
//============================================================ 

airplane_01,240,64,5	duplicate(Exit2)	Saída#05	857

airplane_01,247,64,5	duplicate(Exit2)	Saída#06	857

airplane_01,240,39,1	duplicate(Exit2)	Saída#07	857

airplane_01,247,39,1	duplicate(Exit2)	Saída#08	857

airplane_01,100,69,2	duplicate(Airship Crew)	Tripulação do Aeroplano#02	852

airplane_01,250,59,3	duplicate(AirshipInfo)	Equipe do Aeroplano#info	67

airplane_01,50,66,4	duplicate(AirshipApples)	Meltz	86

airplane_01,32,61,4	duplicate(TypingContest)	Nils	49	

airplane_01,221,158,6	duplicate(Pilot)	Piloto#air-1	852

airplane_01,83,61,2	script	Dianne	72,{
	show "[Dianne]";
	show "É tão estranho!";
	show "Eu fui visitar o";
	show "Capitão do Avião e";
	show "tudo que vi foi essa";
	show "rena estranha. Oh!";
	show "Você acha que...";
	close;
OnTouch:
	Emotion 28, "Dianne";
	end;
}

airplane_01,69,63,2	script	Velho#06	55,{

	show "[Mendel]";
	show "Como eu imaginava, as";
	show "refeições servidas no";
	show "vôo são apenas 3 estrelas.";
	show "^111111*Harrrumph*^000000 Eu devia";
	show "ter trazido meu 'chef'";
	show "para poder comer";
	show "comida de verdade!";
	close;

}

airplane_01,33,68,4	script	Clarice	74,{
	show "[Clarice]";
	show "Oi, Eu sou a Clarice.";
	show "Você gostaria de";
	show "apostar algumas maçãs";
	show "no jogo dos Dados?";
	next;
	callfunc "applegamble","Clarice";
	end;
}

airplane_01,71,31,6	script	Espadachim Shimizu	106,{
	show "[Espadachim Shimizu]";
	show "Finalmente, após cinco";
	show "cinco anos depois";
	show "Eu posso ter minha vingança!";
	next;
	show "[Espadachim Shimizu]";
	show "Eu neste momento...";
	show "Já não posso mais parar.";
	show "Mas em pouco tempo,";
	show "terei minha vingança!";
	close;
}

//============================================================ 
//= Equipe do Aeroplano de Izlude
//============================================================

izlude,201,54,3	script	Equipe do Aeroplano#izl	91,{

	show "[Equipe do Aeroplano]";
	show "Bem vindo ao Aeroporto";
	show "de Izlude.";
	show "Em que posso ajudá-lo?";
	next;
	menu "Embarcar no Aeroplano",-,"Cancelar",L_Cancel;

		show "[Equipe do Aeroplano]";
		show "A passagem para embarcar no";
		show "avião custa 1,200 zeny, mas se";
		show "você tiver um Bilhete para Aeroplano,";
		show "essa taxa não será cobrada.";
		show "Deseja embarcar agora?";
		next;
		menu "Sim",-,"Não",L_Cancel;

		if(countitem(7311) > 0) goto L_GotTicket;
		if(Zeny < 1200) goto L_NoZeny;
			set Zeny, Zeny - 1200;
			warp "airplane_01",224,64;
			close;

		L_GotTicket:
			delitem 7311,1;
			warp "airplane_01",224,64;
			close;

	L_NoZeny:
		show "[Equipe do Aeroplano]";
		show "Você não tem Zeny suficiente.";
		close;

	L_Cancel:
		show "[Equipe do Aeroplano]";
		show "Obrigado e tenha um bom dia.";
		close;
}

//============================================================ 
//= Equipe do Aeroplano de Hugel
//============================================================

hugel,182,150,3	script	Equipe do Aeroplano#hu	91,{

	show "[Equipe do Aeroplano]";
	show "Bem vindo ao Aeroporto de Hugel.";
	show "Em que posso ajudá-lo?";
	next;
	menu "Embarcar no Aeroplano",-,"Cancelar",L_Cancel;

		show "[Equipe do Aeroplano]";
		show "A passagem para embarcar no";
		show "avião custa 1,200 zeny, mas se";
		show "você tiver um Bilhete para Aeroplano,";
		show "essa taxa não será cobrada.";
		show "Deseja embarcar agora?";
		next;
		menu "Sim",-,"Não",L_Cancel;

		if(countitem(7311) > 0) goto L_GotTicket;
		if(Zeny < 1200) goto L_NoZeny;
			set Zeny, Zeny - 1200;
			warp "airplane",224,64;
			close;

		L_GotTicket:
			delitem 7311,1;
			warp "airplane",224,64;
			close;

	L_NoZeny:
		show "[Equipe do Aeroplano]";
		show "Você não tem Zeny suficiente.";
		close;

	L_Cancel:
		show "[Equipe do Aeroplano]";
		show "Obrigado e tenha um bom dia.";
		close;
}

//============================================================ 
//= Equipe do Aeroplano de Rachel
//============================================================

ra_fild12,294,206,3	script	Equipe do Aeroplano#ra	91,{

	show "[Equipe do Aeroplano]";
	show "Bem vindo ao Aeroporto de Rachel.";
	show "Em que posso ajudá-lo?";
	next;
	menu "Embarcar no Aeroplano",-,"Cancelar",L_Cancel;

		show "[Equipe do Aeroplano]";
		show "A passagem para embarcar no";
		show "avião custa 1,200 zeny, mas se";
		show "você tiver um Bilhete para Aeroplano,";
		show "essa taxa não será cobrada.";
		show "Deseja embarcar agora?";
		next;
		menu "Sim",-,"Não",L_Cancel;

		if(countitem(7311) > 0) goto L_GotTicket;
		if(Zeny < 1200) goto L_NoZeny;
			set Zeny, Zeny - 1200;
			warp "airplane_01",224,64;
			close;

		L_GotTicket:
			delitem 7311,1;
			warp "airplane_01",224,64;
			close;

	L_NoZeny:
		show "[Equipe do Aeroplano]";
		show "Você não tem Zeny suficiente.";
		close;

	L_Cancel:
		show "[Equipe do Aeroplano]";
		show "Obrigado e tenha um bom dia.";
		close;
}

//============================================================ 
//= NPCs do Aeroporto de Juno
//============================================================

y_airport,144,63,4	script	Equipe do Aeroporto#Ein	91,{

	show "[Equipe do Aeroporto]";
	show "Bom dia!";
	show "Você gostaria de ir para";
	show "^FF0000Einbroch^000000, ^FF0000Hugel^000000 ou";
	show "^FF0000Lighthalzen^000000?";
	next;
	menu "Sim.",s_Warp,"Não.",-;

	show "[Equipe do Aeroporto]";
	show "Obrigado e tenha um bom dia.";
	close;

s_Warp:
	show "[Equipe do Aeroporto]";
	show "Obrigado e tenha um bom dia.";
	close2;
	warp "yuno",57,240;
	end;

}
	
y_airport,141,63,4	script	Equipe do Aeroporto#Izl	91,{

	show "[Equipe do Aeroporto]";
	show "Bom dia!";
	show "Você gostaria de ir para";
	show "^FF0000Izlude^000000?"; //ou ^FF0000Rachel^000000?";
	next;
	menu "Sim.",s_Warp,"Não.",-;

	show "[Equipe do Aeroporto]";
	show "Obrigado e tenha um bom dia.";
	close;

s_Warp:
	show "[Equipe Aérea]";
	show "Obrigado e tenha um bom dia.";
	close2;
	warp "yuno",50,240;
	end;

}

yuno,14,262,3	script	Equipe do Aeroplano#yuno01::Airship Staff	91,{
	show "[Equipe do Aeroplano]";
	show "Bem-vindo ao Aeroporto de Juno.";
	show "Por favor use esta porta para";
	show "embarcar no Aeroplano que estará";
	show "sempre voando para Izlude";
	show "no Reino de Rune-Midgard.";
	next;
	show "[Equipe do Aeroplano]";
	show "Mas, se Juno for seu destino,";
	show "por favor desça as escadas";
	show "e fale com a Equipe de Funcionários";
	show "para lhe conduzirem ao terminal principal.";
	show "Obrigado e tenha uma boa viagem.";
	close;
}

yuno,88,263,5	duplicate(Airship Staff)	Equipe do Aeroplano#yuno02	91

//============================================================ 
//= Apple Gambling Function
//============================================================

function	script	applegamble	{

	switch(select("Jogar Jogo de Dados:Regras do Jogo:Cancelar")){
	case 3:
		show "["+getarg(0)+"]";
		show "Estarei pronto para";
		show "um joguinho de dados";
		show "sempre que quiser. Me";
		show "procure se estiver";
		show "interessado, ok?";
		close;
	case 2:
		show "["+getarg(0)+"]";
		show "As regras para o jogo de dados";
		show "são muito simples. Primeiro ";
		show "você aposta algumas Maçãs.";
		show "Você pode apostar no máximo 50";
		show "Maçãs por vez. Para que as";
		show "coisas fiquem dentro da lei";
		show "eu apenas aceito Maçãs como";
		show "aposta.";
		next;
		show "["+getarg(0)+"]";
		show "Mas hei, se você tem tanto";
		show "Zeny que está caindo pelos";
		show "bolsos afora, vá até o";
		show "Fruitz e compre quantas";
		show "Maçãs você quiser~";
		next;
		show "["+getarg(0)+"]";
		show "Agora, começamos comigo";
		show "jogando dois dados de 6";
		show "lados. Quando for sua vez";
		show "você também jogará dois";
		show "dados de 6 lados. Depois";
		show "disso nós dois teremos a";
		show "chance de jogar um terceiro";
		show "dado.";
		next;
		show "["+getarg(0)+"]";
		show "Agora uma coisa importante:";
		show "Se você fizer mais que 12";
		show "pontos, você já era, ou seja,";
		show "você perdeu. Caso contrário";
		show "quem fizer mais pontos é o";
		show "vencedor. Entendeu?";
		next;
		show "["+getarg(0)+"]";
		show "Agora, você será o primeiro";
		show "a decidir se joga ou não o";
		show "terceiro dado. Então,";
		show "dependendo do seu resultado";
		show "eu jogo meu terceiro dado...";
		show "ou não...";
		next;
		show "["+getarg(0)+"]";
		show "Quando você ganha, recebe";
		show "o dobro de Maçãs que você";
		show "apostou.";
		show "Se por acaso acontecer de";
		show "empatarmos, você recebe de";
		show "volta todas as Maçãs";
		show "apostadas. Justo, não?";
		close;
	case 1:
		break;
	}
	show "["+getarg(0)+"]";
	show "Ooh, então você vai jogar";
	show "comigo? Ótimo! Quantas";
	show "Maçãs você quer apostar?";
	show "Lembre-se, você pode apostar";
	show "até 50 Maçãs. Se quiser";
	show "cancelar, por favor entre";
	show "com '0' na quantidade.";
	next;
L_Input:
	digit @amount;
	if(@amount == 0) {
		show "["+getarg(0)+"]";
		show "Mudou de idéia?";
		show "Eu entendo. Bem, espero";
		show "que possamos jogar outra";
		show "hora.";
		close;
	}
	if(@amount > 50) set @amount,50;
	show "["+getarg(0)+"]";
	show "Então você quer";
	show "apostar ^FF0000"+@amount+"^000000 Maçãs.";
	show "Certo?";
	next;
	if(select("Sim:Não")==2){
		show "["+getarg(0)+"]";
		show "Hmm, se enganou?";
		show "Tudo bem, por favor entre";
		show "novamente com o número de";
		show "Maçãs que você quer apostar.";
		next;
		goto L_Input;
	}
	if(countitem(512)<@amount){
		//more apples then in inventory
		//-Improvised-
		show "["+getarg(0)+"]";
		show "Ooh...";
		show "Você não tem tantas";
		show "Maçãs com você.";
		close;
	}
	delitem 512,@amount;
	show "["+getarg(0)+"]";
	show "Muito bem!";
	show "Agora podemos iniciar";
	show "o jogo! Eu vou jogar primeiro~";
	next;
	show "^0000FF*Rolando e balançando*";
	set @table1,rand(1,6);
	set @table2,rand(1,6);
	set @tablesub,@table1+@table2;
	next;
	show "Eu tirei um "+@table1+" e um "+@table2+",";
	show "dando um total de "+@tablesub+".";
	show "Agora é a sua vez,";
	show strcharinfo(0)+".";
	next;
	menu "Jogar o Dado.",-;

	show "^0000FF*Rolando e balançando*"; 
	set @player1,rand(1,6);
	set @player2,rand(1,6);
	set @playersub,@player1+@player2;
	next;
	show "["+getarg(0)+"]";
	show strcharinfo(0)+",";
	show "você tirou um "+@player1+" e um "+@player2+",";
	show "dando um total de ^FF0000"+@playersub+"^000000.";
	next;
	show "["+getarg(0)+"]";
	if(@playersub == @tablesub) {
		show "Well, well, well.";
		show "Nós dois tivemos um total";
		show "de "+@playersub+". Bem,";
		show "a bola está com você.";
		show "Você vai jogar o terceiro";
		show "dado, " +strcharinfo(0)+"?";
	} else if(@playersub > @tablesub) {
		show "Como eu fiz apenas ^0000FF"+@tablesub+"^000000 pontos,";
		show "você está em vantagem com";
		show "seus ^FF0000"+@playersub+"^000000 pontos. Você";
		show "quer jogar mais uma vez?";
		show "Lembre-se, você perde se fizer";
		show "mais que 12 pontos em suas";
		show "jogadas.";
	} else if(@tablesub > @playersub) {
		show "Como eu tenho ^0000FF"+@tablesub+"^000000 pontos,";
		show "estou em vantagem no momento,";
		show "porque você tem apenas ^FF0000"+@playersub+"^000000.";
		show "Você que jogar mais uma vez?";
		show "Lembre-se, você perde se fizer";
		show "mais que 12 pontos em suas";
		show "jogadas.";
	}
	next;
	if(select("Jogar mais uma vez.:Não jogar.")==2){
		show "["+getarg(0)+"]";
		show "Não vai jogar, huh?";
		if(@tablesub == @playersub) {
			show "Torça para que eu tire";
			show "um número muito alto e";
			show "perca, ou eu vou fazer";
			show "mais pontos que você,";
			show "certeza! Lá vai...";
			next;
			goto L_Table3;
		} else if(@tablesub < @playersub) {
			next;
			show "["+getarg(0)+"]";
			show "Muito bem, vejo que você";
			show "não quer arriscar passar";
			show "dos 12 pontos e perder.";
			show "Eu vou jogar então.";
			next;
			goto L_Table3;
		} else if(@tablesub > @playersub) {
		//you have a lower sub total then table, and do not roll 3th -Improvised-
			show "["+getarg(0)+"]";
			show "Não vai jogar, huh?";
			show "Bem, eu não vou";
			show "jogar também.";
			next;
			show "["+getarg(0)+"]";
			show "Isso significa que eu";
			show "tenho um total de ^0000FF"+@tablesub+"^000000";
			show "pontos, que é mais que";
			show "os seus ^FF0000"+@playersub+"^000000 pontos.";
			show "Sinto muito, mas você";
			show "perdeu este jogo, "+strcharinfo(0)+".";
			show "Mais sorte na próxima vez.";
		}
		close;
	}
	show "^0000FF*Rolando e balançando*";
	set @player3,rand(1,6);
	set @playersub,@playersub+@player3;
	if(@playersub > 12) {
	//player bust --Improvised--
		next;
		show "["+getarg(0)+"]";
		show "Meu Deus... Você conseguiu um";
		show "^FF000"+@player3+"^000000, dando um total de ^FF0000"+@playersub+"^000000.";
		show "Isto é mais que 12, o que";
		show "significa que você estourou o";
		show "limite. Desculpe, mas você";
		show "perdeu este jogo, "+strcharinfo(0)+".";
		close;
	}
	next;
	show "["+getarg(0)+"]";
	show "Hei! Você tirou um";
	if(@playersub >= @tablesub) {
		show "^FF0000"+@player3+"^000000, dando um total";
		show "de ^FF0000"+@playersub+"^000000 pontos. Agora, se eu";
		show "não jogar eu perco!";
		show "Então lá vai...";
		next;
		goto L_Table3;
	} else if(@playersub < @tablesub) {
		//player's sub together with 3th die is still too low
		//--Improvised--
		show "^FF0000"+@player3+"^000000, dando um total";
		show "de ^FF0000"+@playersub+"^000000.";
		show "Isto não é suficiente para";
		show "bater meus ^0000FF"+@tablesub+"^000000 pontos.";
		show "Desculpe, mas você perdeu";
		show "este jogo, "+strcharinfo(0)+". Vamos";
		show "dar uma pausa, eu suponho...";
		close;
	}
L_Table3:
	show "^0000FF*Rolando e balançando*";
	set @table3,rand(1,6);
	set @tablesub,@tablesub+@table3;
	next;
	if(@tablesub > 12) {
		show "["+getarg(0)+"]";
		show "Hã? Eu tirei um ^0000FF"+@table3+"^000000, dando";
		show "um total de ^0000FF"+@tablesub+"^000000. Detesto";
		show "dizer isso, mas eu perdi.";
		show "Pegue seu prêmio antes";
		show "que eu chore, "+strcharinfo(0)+"~!";
		close2;
		additem 512,@amount*2;
		end;
	}
	show "["+getarg(0)+"]";
	if(@tablesub < @playersub) {
		show "Eu tirei um "+@table3+", que me";
		show "dá um total de ^0000FF"+@tablesub+"^000000.";
		show "Mas... Isto não é suficiente";
		show "para bater seus ^FF0000"+@playersub+"^000000 pontos.";
		show "Parece que eu não posso";
		show "competir com você, "+strcharinfo(0)+"...";
		close2;
		additem 512,@amount*2;
		end;
	} else if(@tablesub > @playersub) {
	show "Eu tirei um ^0000FF"+@table3+"^000000,";
	show "totalizando ^0000FF"+@tablesub+"^000000 pontos,";
	show "o que bate seus ^FF0000"+@playersub+"^000000 pontos.";
	show "Desculpe, mas você perdeu";
	show "este jogo, "+strcharinfo(0)+". Vamos";
	show "dar uma pausa, eu suponho...";
	close;
	} else if(@tablesub == @playersub) {
		//Result = tie, --Improvised--
		show "Eu tirei um ^0000FF"+@table3+"^000000, o que";
		show "totaliza ^0000FF"+@tablesub+"^000000 pontos,";
		show "exatamente o número de pontos";
		show "que você tem.";
		show "Bem, este jogo não teve";
		show "ganhador nem perdedor, "+strcharinfo(0)+".";
		close2;
		additem 512,@amount;
		end;
	}
}