//===== Cronus Script ========================================
//= Quest de Suco
//===== Por: =================================================
//= kobra_k88
//===== Versão Atual: ========================================
//= 1.5b/1.8
//===== Compatível Com: ======================================
//= eAthena 7.15 +
//===== Descrição: ===========================================
//= Primeira parte da Quest de Suco
//===== Cometários Adicionais: ===============================
//= 1.5b Traduzido e Adaptado p/ o Cronus [Pudim]
//============================================================

// Morrison ------------------------------------------------
prt_in,47,173,2	script	Morrisson	97,{
	show "[Pequeno Morrisson]";
	if(MISC_QUEST&1 || @MORRISON > 10) goto L_GoAway;
	if(@MORRISON == 10) goto L_Candy;

	show "Eu já disse, não quero comer essas frutas idiotas!";
	show "Eu quero carne!!!";
	emotion e_sob;
	next;
	menu "Conversar",M_Talk,"Cancelar",M_End;

	M_Talk:
		if(countitem(517)) goto L_Meat; //Carne

		show "[Pequeno Morrisson]";
		show "Que mãe idiota que eu tenho, fica me forçando a comer frutas.";
		show "Eu odeio frutas, elas são duras demais pros meus dentes!";
		show "Carnes são muito mais macias e saborosas.";
		close;

		L_Meat:
		menu "Dar a Carne",sM_Give,"Ignorar",sM_Ignore;
		
		sM_Give:
			if(countitem(517) == 0) goto L_GoAway; //Meat
			delitem 517,1;	//Meat
			set @MORRISON,@MORRISON+1;
			show "[Pequeno Morrisson]";
			show "Woooww~!!! Carne, é Carne de verdade!";
			show "Mmmm! Parece águra na boca.";
			show "Oh Muito Obrigado. Agora estou feliz. Yupp Yupp...";
			emotion e_awsm;
			close;

		sM_Ignore:
			set @MORRISON,11;
			show "[Pequeno Morrisson]";
			show "Boo...";
			show "Boo... Bem... Tudo bem, tudo bem~!!!!";
			emotion e_bzz;
			close;
	M_End:
		show "[Pequeno Morrisson]";
		show "Wahhhh!!! Não tem o porque de eu tentar, eu não posso comer Vegetais";
		show "ou ^0000ffFrutas^000000! Elas não são muito saborosas!!";
		emotion e_sob;
		close;

L_GoAway:
	show "Pffff... Me deixe sozinho! Você NÃO SE importa comigo! Eu vou acabar morrendo por causa desses estupidos vegetais e frutas!";
	close;
L_Candy:
	show "Ohh..Estou cheio agora.";
	show "Você não precisa mais me alimentar. Eu acho que vou explodir se comer mais.";
	next;
	show "[Pequeno Morrisson]";
	show "E... Hmm... Pegue Isso.";
	next;
	additem 529,3;
	additem 530,1;
	set @MORRISON,0;
	show "[Pequeno Morrisson]";
	show "Esse é meu pacote de Doces que eu escondi da minha Mãe.  Se você me der mais Carne, eu te darei mais alguns Doces." ;
	close;

}

// Marianne ---------------------------------------------------
prt_in,49,172,2	script	Marianne	53,{
	show "[Marianne]";
	if(MISC_QUEST&1 || @MORRISON > 10) goto L_Juice;
	if(@MORRISON) goto L_GaveMeat;

	show "Morrisson, meu filhinho, come uma frutinha, faz esse favor";
	show "*Snif*~!! Tal pai tal filho...";
	emotion e_sob;
	next;
	menu "Conversar",M_Talk,"Cancelar",M_Cancel;
	close;	

	M_Talk:
		show "[Marianne]";
		show "(~Sigh~)...Esse é o Morrisson... Meu primeiro e unico filho.";
		show "Ele está passando por uma fase e não quer comer nenhuma ^0000ffFruta^000000 ou vegetal.  Olhe seu rosto, fica assim só de ouvir falar em ^0000fffFruta^000000...";
		next;
		show "[Marianne]";
		show "Tudo que ele quer comer é ^ff0000Carne^000000... ele precisa de ^0000ffFrutas^000000 e vegetais para uma dieta saudável.";
		next;

	M_Cancel:
		show "[Marianne]";
		show "Morrisson!! Eu vou ficar louca se você continuar fazendo isso!";
		emotion e_bzz;
		close;
	
L_GaveMeat:
	set @MORRISON,0;
	show "Ai MEU DEUS!!... Oh Senhor...";
	emotion e_omg;
	next;
	show "[Marianne]";
	show "O QUE VOCÊ ESTÁ FAZENDO!! COMO VOCÊ DESAFIOU alimentar meu filho!! Com ^ff0000CARNE^000000 ainda mais!!!";
	next;
	show "[Marianne]";
	show "Arrrgh~~!! Saia agora!!!!!!";
	emotion e_pif;
	close;

L_Juice:
	show "Obrigado por não dar nenhuma carne ao meu filho.  Estou tentando alimetá-lo com ^0000ffFrutas^000000 e vegetais, mas essas crianças não comen nada exceto Carne...";
	next;
	show "[Marianne]";
	show "Se eu pudesse transformar ^0000ffFrutas^000000 em ^0000ffSucos^000000... por exemplo...Suco de laranja... Será que minha criança iria gostar?";
	next;
	show "[Marianne]";
	show "Ah! Agora eu lembro..Tem uma cidade onde você consegue bastante ^0000ffFrutas^000000 transformadas em ^0000ffSucos de Frutas^000000.";
	next;
	show "[Marianne]";
	show "... Era em ...^ff0000Payon^000000...ou ^ff0000Morroc^000000...?";
	set @MORRISON,0;
	set MISC_QUEST,MISC_QUEST | 1;
	next;
	close;

}
//Marx Hansen
payon_in03,188,146,5	script	Marx Hansen	86,{
	show "[Marxhansen, o Mercador]";
	if(MISC_QUEST&1) goto L_Juice;
	show "Bem-vindo.";
	next;
	menu "Conversar.",-,"Cancelar.",M_End;

		show "[Marxhansen, o Mercador]";
		show "Quando os humanos ainda eram nômades, eles dependiam das frutas que achavam.";
		show "E com o tempo eles aprenderam a fazer sucos com elas, para mante-las por mais tempo.";
		next;
		show "[Marxhansen, o Mercador]";
		show "Agora, temos grandes hortas, e podemos ter todas as frutas que gostaríamos.";
		show "Mas os sucos continuam a ser muito importantes, por conterem apenas o que é bom da fruta.";
		show "Nos sucos, você pode aproveitar um sabor muito mais concentrado.";
		close;

	M_End:
		show "[Marxhansen, o Mercador]";
		show "Você veio até aqui por nada?";
		close;

//--------------
L_Juice:
//---------
		show "Bem vindo à minha loja. Você veio aqui preparar suco de frutas?";
		next;
		menu "Sim.",M_1a,"Por favor, diga-me os requerimentos.",M_1b,"Não, eu não.",M_1End;
	
	M_1a:
			show "[Marxhansen, o Mercador]";
			show "Qual tipo de suco de fruta deseja preparar?";
			next;
			menu "Suco de Maçã",sM_1a,"Suco de Banana",sM_1b,"Suco de Cenoura",sM_1c,"Suco de Uva",sM_1d,"Nenhum, obrigado.",M_1End;

		sM_1a:
			callsub sF_Make, 512, "maçãs", 531;
			goto M_1a;
		sM_1b:
			callsub sF_Make, 513, "bananas", 532;
			goto M_1a;
		sM_1d:
			callsub sF_Make, 514, "uvas", 533;
			goto M_1a;
		sM_1c:
			callsub sF_Make, 515, "cenouras", 534;
			goto M_1a;

	M_1b:
		show "[Marxhansen, o Mercador]";
		show "# Informações dos Sucos de Fruta #";
		show "- Suco de ^FF3355Maça^000000 - 1 Maçã, 1 Garrafa Vazia, 3 zeny.";
		show "- Suco de ^D5A500Banana^000000 - 1 Banana, 1 Garrafa Vazia, 3 zeny.";
		show "- Suco de ^FF8800Cenoura^000000 - 1 Cenoura, 1 Garrafa Vazia, 3 zeny.";
		show "- Suco de ^AA00AAUva^000000 - 1 Uva, 1 Garrafa Vazia, 3 zeny.";
		close;

	M_1End:
		show "[Marxhansen, o Mercador]";
		show "Oh tudo bem... vejo você na próxima vez.";
		close;

sF_Make:
	show "[Marxhansen, o Mercador]";
	show "Quantos? Você precisa...";
	next;
	menu "Eu lhe direi.",M_Tell, "O possível",-, "Eu mudei de idéia!",M_1End;

		set @amount,1000;		//max possible amount of Juice
		//calc max possible # juices
		if(Zeny/3 < @amount) set @amount, Zeny/3;
		if(countitem(getarg(0)) < @amount) set @amount, countitem(getarg(0));
		if(countitem(713) < @amount) set @amount, countitem(713);
		if(@amount > 0) goto L_Make;
		show "[Marxhansen, o Mercador]";
		show "Doh! VOcê não tem ingredientes bastantes para uma única garrafa de suco.";
		emotion e_gasp;
		close;

	M_Tell:
		show "[Marxhansen, o Mercador]";
		show "Muito bem então. Entre com 0 se não quiser nada. Você não pode";
		show "comprar mais de 1000 de uma só vez.";
		next;
		digit @amount;
		if(@amount < 1 || @amount > 1000) return;
		if(countitem(getarg(0)) < @amount) goto sL_NoFruits;
		if(countitem(713) <  @amount) goto sL_NoBottles;
		if(Zeny < @amount * 3) goto sL_NoZeny;

	L_Make:
		delitem getarg(0), @amount;	//del Fruit
		delitem 713, @amount;		//del Empty Bottle
		set Zeny, Zeny - @amount * 3;
		additem getarg(2), @amount;	//get Juice
		show "[Marxhansen, o Mercador]";
		show "Aqui está. Tome.";
		close;

		sL_NoFruits:
			show "[Marxhansen, o Mercador]";
			show "Você não tem a quantidade de frutas necessária.";
			show "Você precisa de " +@amount+ " " +getarg(1)+ " para fazer o suco.";
			next;
			return;
		sL_NoBottles:
			show "[Marxhansen, o Mercador]";
			show "Você não tem as garrafas necessárias ou as frutas para fazer o suco.";
			next;
			return;
		sL_NoZeny:
			show "[Marxhansen, o Mercador]";
			show "Você não tem a quantidade de dinheiro necessário.  Você precisa de " +(@amount*3)+ " zeny, para fazer os sucos que deseja.";
			next;
			return;						
}