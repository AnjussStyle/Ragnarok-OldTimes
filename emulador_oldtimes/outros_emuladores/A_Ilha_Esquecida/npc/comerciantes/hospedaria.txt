//===== Cronus Script =====================================
//= Npcs do INN
//===== Por: ==============================================
//= MaxDragon
//===== Versão Atual: =====================================
//= 1.3/2.6
//===== Descrição: ========================================
//= No INN, Salvar e Curar.
//===== Cometários adicionais: ============================
//= 1.1 Iniciado o Npc [DarkChild]
//= 1.2 Traduzido [Kamper]
//= 1.2.1 Corrigido localização em Payon
//= 1.2.1a Erros de português corrigido por @Coja ImortalRO Staff
//= 1.3 Adicionado Hugel até Veins, obrigado Biali pela ajuda.[MaxDragon]
//=========================================================

//======================== Prontera ===================================
// Hospedaria do Oeste -------------------
prt_in,244,135,2	script	Estalajadeira#Sammy	53,{

	callfunc "F_InnMaid","[Sammy]","Hospedaria do Oeste","prt_in",238,130;
	warp "prt_in",247,104; end;
}
// Hospedaria do Leste ------------------
prt_in,61,141,2	script	Estalajadeira#Ahlma	53,{

	callfunc "F_InnMaid","[Ahlma]","Hospedaria do Leste","prt_in",64,136;
	warp "prt_in",60,166; end;
}

//======================== Alberta ====================================
// Hospedaria do Norte --------------------------
alberta_in,32,142,5	script	Estalajadeira#Moira	53,{

	callfunc "F_InnMaid","[Moira]","Hospedaria do Norte","alberta_in",26,138;
	warp "alberta_in",18,188; end;
}
// Hospedaria do Sul -----------------------------
alberta_in,55,142,8	script	Estalajadeira#Tina	53,{

	callfunc "F_InnMaid","[Tina]","Hospedaria do Sul","alberta_in",60,140;
	warp "alberta_in",68,188; end;
}

//====================== Geffen ======================================
geffen_in,70,64,5	script	Estalajadeira#Cena	53,{

	callfunc "F_InnMaid","[Cena]","Hospedaria","geffen_in",70,59;
	warp "geffen_in",31,31; end;
}

//======================= Payon ======================================
payon_in01,132,62,5	script	Estalajadeira#Ahee	53,{

	callfunc "F_InnMaid","[Ahee]","Hospedaria de Payon","payon_in01",136,61;
	warp "payon_in01",132,11; end;
}

//======================== Morocc ====================================
// Hospedaria do Norte --------------------------
morocc_in,147,141,3	script	Estalajadeira#Hasna	53,{

	callfunc "F_InnMaid","[Hasna]","Hospedaria de Morroc","morocc_in",142,140;
	warp "morocc_in",173,135; end;
}
// Hospedaria do Sul ----------------------------
morocc_in,80,100,5	script	Estalajadeira#Manar	53,{

	callfunc "F_InnMaid","[Manar]","Hospedaria de Morroc","morocc_in",78,95;
	warp "morocc_in",79,123; end;
}

//====================== Aldebaran ===================================
aldeba_in,92,58,5	script	Estalajadeira#Rilim	53,{

	callfunc "F_InnMaid","[Rilim]","Hospedaria de Al De Baran","aldeba_in",92,50;
	warp "aldeba_in",92,112; end;
}

//======================== Rachel ====================================
ra_in01,376,69,4	script	Estalajadeira#Annie	931,{

	callfunc "F_InnMaid","[Annie]","Hospedaria de Rachel","ra_in01",375,58;
	warp "ra_in01",384,128; end;
}

//====================== Lighthalzen =================================
lhz_in02,230,284,4	script	Funcionário do Hotel#01	86,{

    show "[Funcionário do Hotel]";
    show "Bem vindo ao";
    show "Royal Dragon,";
    show "onde você vai encontrar";
    show "as melhores acomodações";
    show "e os melhores serviços.";
    next;
    menu "Salvar",s_Save,"Descansar - 5,000 zeny",s_Rest,"Cancelar",-;

    show "[Funcionário do Hotel]";
    show "Muito obrigado e";
    show "tenha um ótimo dia.";
    close;

s_Save:
    savepoint "lhz_in02",209,275;
    show "[Funcionário do Hotel]";
    show "Muito obrigado, seu";
    show "Ponto de Retorno foi";
    show "salvo aqui, no";
    show "Royal Dragon.";
    close;

s_Rest:
    if(Zeny < 5000)goto s_NEnoughZ;
    set Zeny,Zeny - 5000;
    show "[Funcionário do Hotel]";
    show "Muito obrigado~";
    show "Espero que você aproveite";
    show "sua estadia aqui";
    show "no Royal Dragon.";
    close2;
    warp "lhz_in02",219,150;
    percentheal 100,100;
    sc_start SC_BLESSING,240000,10;
    skilleffect AL_BLESSING,0;
    end;

s_NEnoughZ:
    show "[Funcionário do Hotel]";
    show "Desculpe-me, mas você parece";
    show "não ter zeny o suficiente. Por favor";
    show "volte quando tiver!";
    close;
}

//======================== Hugel ====================================
hu_in01,246,107,3	script	Estalajadeira#Rep	53,{

	callfunc "F_InnMaid","[Recepcionista]","Hospedaria de Hugel","hu_in01",263,95;
	warp "hu_in01",267,5; end;
}

//======================== Veins ====================================
ve_in,157,219,5	script	Chefe da Hospedaria#Rep	709,{
    show "[Chefe da Hospedaria]";
    show "Bom dia~";
    show "Bem vindo a";
    show "mais confortável";
    show "hospedaria em Veins~";
    next;
    menu "Salvar",Msave, "Descansar -> 5000 zeny",Mrent, "Cancelar",Mend;

    Msave:
        show "[Chefe da Hospedaria]";
        show "Seu Ponto de Retorno";
        show "foi salvo em Veins.";
        show "Aproveite sua estadia na cidade~";
        savepoint "ve_in",157,209;
        close;
    Mrent:
        show "[Chefe da Hospedaria]";
        if(Zeny < 5000){
            show "Desculpe-me, mas você não tem zeny o suficiente para o check-in.";
            show "O valor da estadia é de 5,000 zeny.";
            close;
        }
        show "Aproveite sua estadia~";
        close2;
        set Zeny,Zeny - 5000;
        percentheal 100,100;
        warp "ve_in",184,228;

    Mend:
        close;
}


//======================= Inn Function ==============================
function	script	F_InnMaid	{
	show getarg(0);
	show "Seja bem-vindo à";
	show getarg(1) + ".";
	show "O que posso fazer por você?";
	next;
	menu "Salvar",Msave, "Alugar um Quarto -> 5000 zeny",Mrent, "Nada",Mend;

	Msave:
		show getarg(0);
		show "Local Salvo.";
		show "Se quiser mais alguma coisa basta falar comigo.";
		savepoint getarg(2),getarg(3),getarg(4);
		close;
	Mrent:
		show getarg(0);
		if(Zeny < 5000){
			show "Sinto muito, mas você não tem a quantidade de Zeny necessária.";
			close;
		}
		show "Obrigado e aprecie sua estadia aqui.";
		close2;
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		return;

	Mend:
		close;
}